<?xml version="1.0" encoding="UTF-8"?>
<HBRRepo>
  <variables>
    <variable id="1" name="ГОД" product="Planning" type="member" usage="const">
      <property name="array_type">string</property>
      <property name="dimensionInputMode">type</property>
      <property name="dimensionType">Year</property>
      <property name="prompt_text">Выберите Год</property>
      <property name="scope">ruleset</property>
      <value>FY18</value>
    </variable>
    <variable id="2" name="ВЕРСИЯ" product="Planning" type="member" usage="const">
      <property name="array_type">string</property>
      <property name="dimensionInputMode">type</property>
      <property name="dimensionType">Version</property>
      <property name="prompt_text">Выберите Версию</property>
      <property name="scope">ruleset</property>
      <value>Рабочая_1</value>
    </variable>
    <variable id="3" name="СЦЕНАРИЙ" product="Planning" type="member" usage="const">
      <property name="array_type">string</property>
      <property name="dimensionInputMode">type</property>
      <property name="dimensionType">Scenario</property>
      <property name="prompt_text">Выберите Сценарий</property>
      <property name="scope">ruleset</property>
      <value>Проект</value>
    </variable>
    <variable id="4" name="МВЗ" product="Planning" type="member" usage="const">
      <property name="array_type">string</property>
      <property name="dimension">МВЗ</property>
      <property name="dimensionInputMode">name</property>
      <property name="prompt_text">Выберите МВЗ</property>
      <property name="scope">ruleset</property>
      <value>"Итого по МВЗ"</value>
    </variable>
  </variables>
  <rulesets></rulesets>
  <rules>
    <rule id="1" name="13.6.БАЛАНС" product="Planning">
      <property name="application">ТЧМ</property>
      <property name="plantype">Plan1</property>
      <property name="display_label">13.6.БАЛАНС</property>
      <property name="enablePostProcessing">false</property>
      <property name="enablePreProcessing">false</property>
      <variable_references>
        <variable_reference id="2" name="ВЕРСИЯ">
          <property name="hasvalue">true</property>
          <property name="hidden">false</property>
          <property name="rule_name">13.6.БАЛАНС</property>
          <property name="seq">1</property>
          <property name="type">3</property>
          <property name="useAsOverrideValue">false</property>
        </variable_reference>
        <variable_reference id="3" name="СЦЕНАРИЙ">
          <property name="hasvalue">true</property>
          <property name="hidden">false</property>
          <property name="rule_name">13.6.БАЛАНС</property>
          <property name="seq">2</property>
          <property name="type">3</property>
          <property name="useAsOverrideValue">false</property>
        </variable_reference>
        <variable_reference id="1" name="ГОД">
          <property name="hasvalue">true</property>
          <property name="hidden">false</property>
          <property name="rule_name">13.6.БАЛАНС</property>
          <property name="seq">3</property>
          <property name="type">3</property>
          <property name="useAsOverrideValue">false</property>
        </variable_reference>
        <variable_reference id="4" name="МВЗ">
          <property name="hasvalue">true</property>
          <property name="hidden">false</property>
          <property name="rule_name">13.6.БАЛАНС</property>
          <property name="seq">4</property>
          <property name="type">3</property>
          <property name="useAsOverrideValue">false</property>
        </variable_reference>
      </variable_references>
      <script><![CDATA[SET AGGMISSG ON;
SET UPDATECALC OFF;
SET CREATEBLOCKONEQ OFF;
SET CREATENONMISSINGBLK OFF;
SET FRMLBOTTOMUP OFF;
SET REMOTECALC OFF;
SET CALCPARALLEL 1;
SET CALCTASKDIMS 1;
SET EMPTYMEMBERSETS ON;
SET FRMLRTDYNAMIC OFF;
SET MSG INFO;
/* Проверка корректности рассчитываемого среза данных */

FIX(
/* ВАЛЮТА							*/	"бу валюты"
/* ПЕРИОД							*/	"P0"
/* ГОД								*/	"No Year"
/* ВЕРСИЯ							*/	
/* СЦЕНАРИЙ							*/	
/* ЦФО								*/	"бу ЦФО"
/* МВЗ								*/	"бу МВЗ"
/* МВЗ ПОЛУЧАТЕЛЬ					*/	"бу МВЗ (пол-ль)"
/* СЧЕТА БАЛАНСА					*/	"80_Увеличение"
/* СТАТЬИ							*/	
/* ПОКАЗАТЕЛИ						*/	"Сумма"
/* ВИД ЗАТРАТ						*/	"бу видов затрат"
/* ПРОДУКЦИЯ						*/	"бу вида продукции"
/* КОНТРАГЕНТЫ						*/	"бу контрагента"
/* НАПРАВЛЕНИЕ	*/	"бу направления"
/* ЭЛЕМЕНТЫ_ЗАТРАТ					*/	"бу элемента затрат"
/* ПРОЧЕЕ							*/	"бу прочих справочников"

)
  SET CREATENONMISSINGBLK ON;

  
  var ErrFlagScenario=0;
  var ErrFlagVersion=0;
  var NextM=2;
  /* ПРОВЕРКА КОРРЕКТНОСТИ ВЫБОРА СЦЕНАРИЯ */
  FIX(/* Сценарий */ {СЦЕНАРИЙ} AND @LIST(&ОТКРЫТЫЕ_СЦЕНАРИИ))
    FIX(
    /* Сценарий */ 
    /* Версия   */ "Рабочая 1"
    )
     "бу статьи" (
     IF (ErrFlagScenario==0)
            ErrFlagScenario=1;
     ENDIF
     );

    ENDFIX
  ENDFIX

   /* ПРОВЕРКА КОРРЕКТНОСТИ ВЫБОРА ВЕРСИИ */
  FIX(/* Версия  */ {ВЕРСИЯ} AND @LIST(&ОТКРЫТЫЕ_ВЕРСИИ))
    FIX(
    /* Сценарий */ "Проект"
    /* Версия   */ 
    )
     "бу статьи" (
     IF (ErrFlagVersion==0)
            ErrFlagVersion=1;
     ENDIF
     );
    ENDFIX
  ENDFIX

  /* ВЫВОД СООБЩЕНИЙ О НЕКОРРЕКТНОМ СРЕЗЕ */
  FIX(
  /* Сценарий     */ "Проект"
  /* Версия       */ "Рабочая 1"
  )

   "бу статьи" (
    IF (ErrFlagScenario==0)
       @RETURN("Выбран сценарий, закрытый для расчета. Расчет не произведен.",ERROR);
    ENDIF
    IF (ErrFlagVersion==0)
       @RETURN("Выбрана версия, закрытая для расчета. Расчет не произведен.",ERROR);
    ENDIF
    )

   ENDFIX
ENDFIX
SET CREATENONMISSINGBLK OFF;
FIX(/*МВЗ 							*/@LIST("ТЧМС в целом", "ТЧМ в целом") and @RELATIVE({МВЗ},0)
)
FIX(
/*НАПРАВЛЕНИЕ 	*/
/*МВЗ ПОЛУЧАТЕЛЬ 					*/"бу МВЗ (пол-ль)"
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/
/*МВЗ 								*/
/*СЦЕНАРИЙ 							*/{СЦЕНАРИЙ}
/*ГОД 								*/{ГОД}
/*ВАЛЮТА 							*//*@RELATIVE("Валюты ввода",0)*/RUR
/*ВЕРСИЯ 							*/{ВЕРСИЯ}
/*ЦФО 								*/
/*СЧЕТА БАЛАНСА 					*/
/*ПРОЧЕЕ 							*/
/*КОНТРАГЕНТЫ 						*/
/*ВИД ЗАТРАТ 						*/
/*СТАТЬИ 							*/
/*ПРОДУКЦИЯ 						*/
/*ПОКАЗАТЕЛИ 						*/
/*ПЕРИОД 							*/@RELATIVE("Итого год",0)
)
SET CALCPARALLEL 6;
SET CALCTASKDIMS 15;
FIX(
/*ЦФО 								*/@RELATIVE("Итого по ЦФО",0)
/*ВИД ЗАТРАТ 						*/
/*СЧЕТА БАЛАНСА 					*/
)
/*Зачищаем расчетные статьи баланса*/
FIX(
/*СТАТЬИ 							*/"Амортизация ОС и НМА."
                                      "Незавершенные капитальные вложения"
                                      "Расчеты с поставщиками за оборудование"
                                      "Расчеты с поставщиками за материалы"
                                      "Расчеты с поставщиками и подрядчиками за СМР"
                                      "Расчеты с поставщиками и подрядчиками за ПИР"
                                      "Расчеты с поставщиками и подрядчиками за прочие услуги"
                                      "КЗ перед поставщиками за оборудование"
                                      "КЗ перед поставщиками за материалы (инвест.деят.)"
                                      "КЗ перед поставщиками и подрядчиками за СМР (инвест.деят.)"
                                      "КЗ перед поставщиками и подрядчиками за ПИР (инвест.деят.)"
                                      "КЗ перед поставщиками и подрядчиками за прочие услуги (инвест.деят.)"
                                      "Расчеты с покупателями и заказчиками"
                                      "Авансы полученные от покупателей и заказчиков"
                                      "Расчеты с поставщиками прочие"
                                      "КЗ перед поставщиками прочая"
                                      "Расчеты с поставщиками и подрядчиками по расходам на содержание и ремонт"
                                      "КЗ перед поставщиками по расходам на содержание и ремонт"
                                      "Расчеты с поставщиками за сырье, материалы, топливо"
                                      "КЗ перед поставщиками за сырье, материалы, топливо"
                                      "Сырье и материалы на технологию"
                                       "Заемные средства (долгосрочные)"
                                       "Заемные средства (краткосрочные)"
                                       "Резервы предстоящих расходов и платежей (долгосрочные)"
                                       "Резервы предстоящих расходов и платежей (краткосрочные)"
                                       "Прочие долгосрочные резервы"
                                       "Прочие краткосрочные резервы"
                                       "Резервы по займам выданным долгосрочным"
                                       "Резервы по займам выданным краткосрочным"
                                       "Расчеты с бюджетом и внебюджетными фондами"
                                       "КЗ перед бюджетом и внебюджетными фондами"
                                       "НДС по приобретенным ценностям"
                                       "НДС по авансам полученным"
                                        "Расчеты с персоналом"
                                        "КЗ перед персоналом"
                                        "РБП"
)

CLEARBLOCK ALL;
ENDFIX

/*Переносим 11.2.ОС и НМА*/
FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
/*ПРОДУКЦИЯ 						*/"бу вида продукции"
/*НАПРАВЛЕНИЕ 	*/"бу направления"
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/"бу элемента затрат"
/*КОНТРАГЕНТЫ 						*/"бу контрагента"
/*ПРОЧЕЕ 							*/
/*СЧЕТА БАЛАНСА 					*/"01_Ост на конец (перв ст-ть)","02_Ост на конец (аморт-ия)"
/*ВИД ЗАТРАТ 						*/"Амортизация ОС и НМА"
/*СТАТЬИ 							*/
)

FIX(
/*ПРОЧЕЕ 							*/"Нематериальные поисковые активы"
)
DATACOPY "Нематериальные активы" to "Амортизация ОС и НМА.";
ENDFIX
FIX(
/*ПРОЧЕЕ 							*/@REMOVE(@RELATIVE("Всего по группам ОС и НМА",0),"Нематериальные поисковые активы")
)
DATACOPY "Нематериальные активы" to "Амортизация ОС и НМА.";
ENDFIX

ENDFIX

/*Незавершенные капитальные вложения*/
FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
/*ПРОДУКЦИЯ 						*/"бу вида продукции"
/*НАПРАВЛЕНИЕ 	*/"бу направления"
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/@RELATIVE("Всего по проектам",0)
/*КОНТРАГЕНТЫ 						*/"Прочие внешние"
/*ПРОЧЕЕ 							*/"бу прочих справочников"
/*СЧЕТА БАЛАНСА 					*/"08_Остаток на конец"
/*ВИД ЗАТРАТ 						*/"бу видов затрат"
/*СТАТЬИ 							*/
)


DATACOPY "бу статьи" to "Незавершенные капитальные вложения";

ENDFIX
/*Авансы выданные поставщикам и подрядчикам (инвест):
Кредиторская задолженность по инвест
*/
FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
/*ПРОДУКЦИЯ 						*/"бу вида продукции"
/*НАПРАВЛЕНИЕ 	*/"бу направления"
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/@RELATIVE("Всего по проектам",0)
/*КОНТРАГЕНТЫ 						*/
/*ПРОЧЕЕ 							*/"бу прочих справочников"
/*СЧЕТА БАЛАНСА 					*/"60_ДЗ/КЗ на конец (+/-)"
/*ВИД ЗАТРАТ 						*/"бу видов затрат"
/*СТАТЬИ 							*/"СМР","ПИР","Оборудование","Материалы","Услуги стор.организаций"
)

"Прочие внешние"(
IF("Прочие внешние">0)
IF(@ISMBR("Оборудование"))
"Прочие внешние"->"Расчеты с поставщиками за оборудование"="Прочие внешние"->"Расчеты с поставщиками за оборудование"+"Прочие внешние";
ELSEIF(@ISMBR("Материалы"))
"Прочие внешние"->"Расчеты с поставщиками за материалы"="Прочие внешние"->"Расчеты с поставщиками за материалы"+"Прочие внешние";
ELSEIF(@ISMBR("СМР"))
"Прочие внешние"->"Расчеты с поставщиками и подрядчиками за СМР"="Прочие внешние"->"Расчеты с поставщиками и подрядчиками за СМР"+"Прочие внешние";
ELSEIF(@ISMBR("ПИР"))
"Прочие внешние"->"Расчеты с поставщиками и подрядчиками за ПИР"="Прочие внешние"->"Расчеты с поставщиками и подрядчиками за ПИР"+"Прочие внешние";
ELSEIF(@ISMBR("Услуги стор.организаций"))
"Прочие внешние"->"Расчеты с поставщиками и подрядчиками за прочие услуги"="Прочие внешние"->"Расчеты с поставщиками и подрядчиками за прочие услуги"+"Прочие внешние";
ENDIF
ELSE
IF(@ISMBR("Оборудование"))
"Прочие внешние"->"КЗ перед поставщиками за оборудование" = "Прочие внешние"->"КЗ перед поставщиками за оборудование"-"Прочие внешние";
ELSEIF(@ISMBR("Материалы"))
"Прочие внешние"->"КЗ перед поставщиками за материалы (инвест.деят.)"="Прочие внешние"->"КЗ перед поставщиками за материалы (инвест.деят.)"-"Прочие внешние";
ELSEIF(@ISMBR("СМР"))
"Прочие внешние"->"КЗ перед поставщиками и подрядчиками за СМР (инвест.деят.)"="Прочие внешние"->"КЗ перед поставщиками и подрядчиками за СМР (инвест.деят.)"-"Прочие внешние";
ELSEIF(@ISMBR("ПИР"))
"Прочие внешние"->"КЗ перед поставщиками и подрядчиками за ПИР (инвест.деят.)"="Прочие внешние"->"КЗ перед поставщиками и подрядчиками за ПИР (инвест.деят.)"-"Прочие внешние";
ELSEIF(@ISMBR("Услуги стор.организаций"))
"Прочие внешние"->"КЗ перед поставщиками и подрядчиками за прочие услуги (инвест.деят.)"="Прочие внешние"->"КЗ перед поставщиками и подрядчиками за прочие услуги (инвест.деят.)"-"Прочие внешние";
ENDIF 
ENDIF)
ENDFIX


/*Финансы и займы */
FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
/*ПРОДУКЦИЯ 						*/"бу вида продукции"
/*НАПРАВЛЕНИЕ 	*/@RELATIVE("Всего по срочности",0)
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/"бу элемента затрат"
/*КОНТРАГЕНТЫ 						*/@RELATIVE("Итого по контрагентам",0)
/*ПРОЧЕЕ 							*/@RELATIVE("Всего по видам финвложений",0)
/*СЧЕТА БАЛАНСА 					*/"58_Остаток на конец","58_Ост на конец (+/-)"
/*ВИД ЗАТРАТ 						*/
/*СТАТЬИ 							*/"бу статьи"
)
"бу видов затрат"(
IF(@ISMBR("Акции и вклады в УК (инвестиции)"))
"бу видов затрат"->"Инвестиции."="бу видов затрат"->"Акции и вклады в УК (инвестиции)";
ELSEIF(@ISMBR("Акции и вклады в УК (для продажи)"->Долгосрочные))
"бу видов затрат"->"Долгосрочные финвложения для продажи"="бу видов затрат"->"Акции и вклады в УК (для продажи)";
ELSEIF(@ISMBR("Депозиты."->Долгосрочные))
"бу видов затрат"->"Депозиты долгосрочные"="бу видов затрат";
ELSEIF(@ISMBR("Облигации стор.организаций"->Долгосрочные) or @ISMBR("Прочие фин.инструменты"->Долгосрочные))
"бу видов затрат"->"Прочие финвложения долгосрочные"="бу видов затрат";
ELSEIF(@ISMBR("Займы выданные."->Долгосрочные))
"бу видов затрат"->"Займы выданные (долгосрочные)"="бу видов затрат";
ELSEIF(@ISMBR("Акции и вклады в УК (для продажи)"->Краткосрочные))
"бу видов затрат"->"Активы для продажи (краткосрочные)"="бу видов затрат";
ELSEIF(@ISMBR("Депозиты."->Краткосрочные))
"бу видов затрат"->"Депозиты (краткосрочные)"="бу видов затрат";
ELSEIF(@ISMBR("Облигации стор.организаций"->Краткосрочные) or @ISMBR("Прочие фин.инструменты"->Краткосрочные))
"бу видов затрат"->"Прочие финвложения (краткосрочные)"="бу видов затрат";
ELSEIF(@ISMBR("Займы выданные."->Краткосрочные))
"бу видов затрат"->"Займы выданные (краткосрочные)"="бу видов затрат";
ENDIF )
ENDFIX


/*Расчеты с покупателями и заказчиками
Авансы полученные от покупателей и заказчиков
 */
 
FIX(

/*ПРОДУКЦИЯ 						*/@RELATIVE("Всего по продукции",0)
/*НАПРАВЛЕНИЕ 	*/@RELATIVE("Всего по направл. отгрузки",0),"бу направления"
/*ВИД ЗАТРАТ 						*/@RELATIVE("Всего по складам",0),"бу видов затрат"
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/"бу элемента затрат"
/*КОНТРАГЕНТЫ 						*/@RELATIVE("Итого по контрагентам",0)
/*ПРОЧЕЕ 							*/@RELATIVE("Всего по типам договоров",0),"бу прочих справочников"
/*СЧЕТА БАЛАНСА 					*
/*СТАТЬИ 							*/
)
FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
/*СЧЕТА БАЛАНСА 					*/"62_ДЗ на конец"
)

DATACOPY "Реализация осн продукции на внутр рынок" to "Расчеты с покупателями и заказчиками";
DATACOPY "Реализация продукции вспомогательных цехов"  to "Расчеты с покупателями и заказчиками";
DATACOPY "Реализация попутной продукции" to "Расчеты с покупателями и заказчиками";
DATACOPY "Выполнение работ, оказание услуг" to "Расчеты с покупателями и заказчиками";
DATACOPY "Реализация энергоресурсов" to "Расчеты с покупателями и заказчиками";
DATACOPY "Перепродажа продукции" to "Расчеты с покупателями и заказчиками";
DATACOPY "Поступления от прочей реализации" to "Расчеты с покупателями и заказчиками";
DATACOPY "Реализация осн продукции на экспорт" to "Расчеты с покупателями и заказчиками";
DATACOPY "Реализация осн продукции в СНГ" to "Расчеты с покупателями и заказчиками";
ENDFIX

FIX(
/*СТАТЬИ 							*/"Расчеты с покупателями и заказчиками"
/*ПОКАЗАТЕЛИ 						*/
/*СЧЕТА БАЛАНСА 					*/"62_ДЗ на конец"
)
"Сумма"(
 "Сумма"-> "Реализация осн продукции на внутр рынок"
+ "Сумма"-> "Реализация продукции вспомогательных цехов"
+ "Сумма"-> "Реализация попутной продукции"
+ "Сумма"-> "Выполнение работ, оказание услуг"
+ "Сумма"-> "Реализация энергоресурсов"
+ "Сумма"-> "Перепродажа продукции"
+ "Сумма"->"Поступления от прочей реализации"
+ "Сумма"->"Реализация осн продукции на экспорт"
+ "Сумма"->"Реализация осн продукции в СНГ";
)
ENDFIX

FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
/*СЧЕТА БАЛАНСА 					*/"62_КЗ на конец"
)

DATACOPY "Реализация осн продукции на внутр рынок" to "Авансы полученные от покупателей и заказчиков";
DATACOPY "Реализация продукции вспомогательных цехов"  to "Авансы полученные от покупателей и заказчиков";
DATACOPY "Реализация попутной продукции" to "Авансы полученные от покупателей и заказчиков";
DATACOPY "Выполнение работ, оказание услуг" to "Авансы полученные от покупателей и заказчиков";
DATACOPY "Реализация энергоресурсов" to "Авансы полученные от покупателей и заказчиков";
DATACOPY "Перепродажа продукции" to "Авансы полученные от покупателей и заказчиков";
DATACOPY "Поступления от прочей реализации" to "Авансы полученные от покупателей и заказчиков";
DATACOPY "Реализация осн продукции на экспорт" to "Авансы полученные от покупателей и заказчиков";
DATACOPY "Реализация осн продукции в СНГ" to "Авансы полученные от покупателей и заказчиков";
ENDFIX

FIX(
/*СТАТЬИ 							*/"Авансы полученные от покупателей и заказчиков"
/*ПОКАЗАТЕЛИ 						*/
/*СЧЕТА БАЛАНСА 					*/"62_КЗ на конец"
)

"Сумма"(
 "Сумма"->"Реализация осн продукции на внутр рынок"
+"Сумма"->"Реализация продукции вспомогательных цехов"
+"Сумма"->"Реализация попутной продукции"
+"Сумма"->"Выполнение работ, оказание услуг"
+"Сумма"->"Реализация энергоресурсов"
+"Сумма"->"Перепродажа продукции"
+"Сумма"->"Поступления от прочей реализации"
+"Сумма"->"Реализация осн продукции на экспорт"
+"Сумма"->"Реализация осн продукции в СНГ";
)

ENDFIX
ENDFIX

/*Статьи из формы 5.1 (60_ДЗ/КЗ)*/
FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
/*ПРОДУКЦИЯ 						*/"бу вида продукции"
/*НАПРАВЛЕНИЕ 	*/
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/"бу элемента затрат"
/*КОНТРАГЕНТЫ 						*/"Прочие внешние","УК ПМХ."
/*ПРОЧЕЕ 							*/"бу прочих справочников"
/*СЧЕТА БАЛАНСА 					*/"60_ДЗ/КЗ на конец (+/-)"
/*ВИД ЗАТРАТ 						*/"бу видов затрат","Услуги по управлению",@RELATIVE("Прочие расходы",0),@RELATIVE("Обслуж. пр-ва и хозяйства",0)  
/*СТАТЬИ 							*/@RELATIVE("Таможенные расходы",0)
									  @RELATIVE("Страхование.",0)
									  "Услуги УК ПМХ"
									  "Работы и услуги производственного характера"
									  "Прочие выплаты (гр)"
									  "Отрицательная курсовая разница от продажи валюты"
									  "Расходы на социальные объекты"
)
"бу направления"(
IF("бу направления">0)
"бу направления"->"Расчеты с поставщиками прочие"="бу направления"->"Расчеты с поставщиками прочие"+"бу направления";
ELSE
"бу направления"->"КЗ перед поставщиками прочая"= "бу направления"->"КЗ перед поставщиками прочая"-"бу направления";
ENDIF;)

ENDFIX

/*Статьи из формы 5.2 (ДЗ (+), КЗ (-))*/
FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
/*ПРОДУКЦИЯ 						*/"бу вида продукции"
/*НАПРАВЛЕНИЕ 	*/
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/"бу элемента затрат"
/*КОНТРАГЕНТЫ 						*/@RELATIVE("Итого по контрагентам",0)
/*ПРОЧЕЕ 							*/"бу прочих справочников"
/*СЧЕТА БАЛАНСА 					*/"ДЗ (+), КЗ (-) на начало."
/*ВИД ЗАТРАТ 						*/"Ремонт силами подрядчиков",
									  "Материалы на ремонт",
									  "Вспомогательные материалы",
                                     "Материалы для упаковки проката",
                                      "Материалы для маркировки проката",
                                     "Сменный инструмент",
                                     "Сменное оборудование",
									"Инвентарь",
									"Тара и тарные материалы",
									"Запчасти",
									"Прочие материалы",
									@RELATIVE("Прочие МПЗ",0),"ТО силами подрядчиков"

/*СТАТЬИ 							*/"Некапитализируемые капитальные ремонты",
									  "Текущие ремонты",
									  "Техническое обслуживание.",
									  "Малоценные активы",
									  "Сменный инструмент и оборудование",
									  "Прочие затраты на содержание"

)
"бу направления"(
IF("бу направления">0)
"бу направления"->"Расчеты с поставщиками и подрядчиками по расходам на содержание и ремонт"=
										"бу направления"->"Расчеты с поставщиками и подрядчиками по расходам на содержание и ремонт"+"бу направления";
ELSE
"бу направления"->"КЗ перед поставщиками по расходам на содержание и ремонт"= 
									 "бу направления"->"КЗ перед поставщиками по расходам на содержание и ремонт" - "бу направления";
ENDIF;)

ENDFIX


/*Статьи из формы 6.7 (60_ДЗ/КЗ на конец (+/-))*/
FIX(
/*ПОКАЗАТЕЛИ 						*/
/*ПРОДУКЦИЯ 						*/"бу вида продукции"
/*НАПРАВЛЕНИЕ 	*/
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/"бу элемента затрат"
/*КОНТРАГЕНТЫ 						*/@RELATIVE("Итого по контрагентам",0)
/*ПРОЧЕЕ 							*/"бу прочих справочников"
/*СЧЕТА БАЛАНСА 					*/"60_ДЗ/КЗ на конец (+/-)"
/*ВИД ЗАТРАТ 						*/@RELATIVE("Сырье и основные материалы",0)
/*СТАТЬИ 							*/
									  
)
FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
/*НАПРАВЛЕНИЕ 	*/"бу направления"
)
"Расчеты с поставщиками за сырье, материалы, топливо"=#missing;
"КЗ перед поставщиками за сырье, материалы, топливо"=#missing;

DATACOPY "Сырье." to "Расчеты с поставщиками за сырье, материалы, топливо";
DATACOPY "Топливо." to "Расчеты с поставщиками за сырье, материалы, топливо";
DATACOPY "Материалы на технологические цели." to "Расчеты с поставщиками за сырье, материалы, топливо";
DATACOPY "Энергоресурсы_" to "Расчеты с поставщиками за сырье, материалы, топливо";

DATACOPY "Сырье." to "КЗ перед поставщиками за сырье, материалы, топливо";
DATACOPY "Топливо." to "КЗ перед поставщиками за сырье, материалы, топливо";
DATACOPY "Материалы на технологические цели." to "КЗ перед поставщиками за сырье, материалы, топливо";
DATACOPY "Энергоресурсы_" to "КЗ перед поставщиками за сырье, материалы, топливо";

ENDFIX

FIX(
/*ПОКАЗАТЕЛИ 						*/
/*НАПРАВЛЕНИЕ 	*/"бу направления"
/*Статья							*/"Сырье.","Топливо.","Материалы на технологические цели.","Энергоресурсы_"
)


"Сумма"(
IF("Сумма">0)
"Сумма"->"Расчеты с поставщиками за сырье, материалы, топливо"= "Сумма"+"Сумма"->"Расчеты с поставщиками за сырье, материалы, топливо";
									 
ELSEIF("Сумма"<0)
"Сумма"->"КЗ перед поставщиками за сырье, материалы, топливо"= "Сумма"-"Сумма"->"КЗ перед поставщиками за сырье, материалы, топливо";
ENDIF;)

ENDFIX
ENDFIX	 

/*
Сырье и материалы на технологию
Статьи из формы 6.4
Статьи из формы 6.12 подтягиваются сами
 */
FIX(
/*ПОКАЗАТЕЛИ 						*/
/*ПРОДУКЦИЯ 						*/"бу вида продукции"
/*НАПРАВЛЕНИЕ 	*/"бу направления"
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/"бу элемента затрат"
/*КОНТРАГЕНТЫ 						*/@RELATIVE("Итого по контрагентам",0)
/*ПРОЧЕЕ 							*/"бу прочих справочников"
/*СЧЕТА БАЛАНСА 					*/"10_Остаток на конец"
/*ВИД ЗАТРАТ 						*/@RELATIVE("Сырье и основные материалы",0),"бу видов затрат"
/*СТАТЬИ 							*/
)

FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
)
DATACOPY "Прочие затраты на содержание" to "Сырье и материалы на технологию";
DATACOPY "Некапитализируемые капитальные ремонты" to "Сырье и материалы на технологию";
DATACOPY "Текущие ремонты" to "Сырье и материалы на технологию";
DATACOPY "Техническое обслуживание." to "Сырье и материалы на технологию";
DATACOPY "Малоценные активы" to "Сырье и материалы на технологию";
DATACOPY "Сменный инструмент и оборудование" to "Сырье и материалы на технологию";
DATACOPY "Энергоресурсы_" to "Сырье и материалы на технологию";
DATACOPY "Сырье." to "Сырье и материалы на технологию";
DATACOPY "Топливо." to "Сырье и материалы на технологию";
DATACOPY "Материалы на технологические цели." to "Сырье и материалы на технологию";


ENDFIX

FIX(
/*СТАТЬИ 							*/"Сырье и материалы на технологию"
)
"Сумма" = @SUMRANGE("Сумма",@LIST("Сырье.","Топливо.","Материалы на технологические цели.","Прочие затраты на содержание","Некапитализируемые капитальные ремонты","Текущие ремонты","Техническое обслуживание.","Малоценные активы","Сменный инструмент и оборудование","Энергоресурсы_","Сырье.","Топливо.","Материалы на технологические цели."));
ENDFIX


ENDFIX
/*Статьи из формы 9.2 - 9.3
"Заемные средства (долгосрочные)"
"Заемные средства (краткосрочные)"

)*/
FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
/*ПРОДУКЦИЯ 						*/"бу вида продукции"
/*НАПРАВЛЕНИЕ 	*/
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/"бу элемента затрат"
/*КОНТРАГЕНТЫ 						*/@RELATIVE("Итого по контрагентам",0)
/*ПРОЧЕЕ 							*/@RELATIVE("Всего по обязательствам",0)
/*СЧЕТА БАЛАНСА 					*/"66_Ост на конец","66_Ост % на конец"
/*ВИД ЗАТРАТ 						*/"бу видов затрат"
/*СТАТЬИ 							*/
									  
)


DATACOPY "бу статьи"->"Долгосрочные" to "Заемные средства (долгосрочные)"->"Долгосрочные";
DATACOPY "бу статьи"->"Краткосрочные" to "Заемные средства (краткосрочные)"->"Краткосрочные";


ENDFIX

/*
Резервы предстоящих расходов и платежей (долгосрочные)
Резервы предстоящих расходов и платежей (краткосрочные)
Прочие долгосрочные резервы
Прочие краткосрочные резервы
Резервы по займам выданным долгосрочным
Резервы по займам выданным краткосрочным


Статьи из формы 9.5
 */
FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
/*ПРОДУКЦИЯ 						*/"бу вида продукции"
/*НАПРАВЛЕНИЕ 	*/
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/"бу элемента затрат"
/*КОНТРАГЕНТЫ 						*/"бу контрагента"
/*ПРОЧЕЕ 							*/"бу прочих справочников"
/*СЧЕТА БАЛАНСА 					*/"96_Остаток на конец"
/*ВИД ЗАТРАТ 						*/"бу видов затрат"
/*СТАТЬИ 							*/
)

DATACOPY "Резервы предстоящих расходов и платежей"->"Долгосрочные" to "Резервы предстоящих расходов и платежей (долгосрочные)"->"Долгосрочные";
DATACOPY "Резервы предстоящих расходов и платежей"->"Краткосрочные" to "Резервы предстоящих расходов и платежей (краткосрочные)"->"Краткосрочные";

DATACOPY "Прочие резервы"->"Долгосрочные" to "Прочие долгосрочные резервы"->"Долгосрочные";
DATACOPY "Прочие резервы"->"Краткосрочные" to "Прочие краткосрочные резервы"->"Краткосрочные";

DATACOPY "Прочие резервы"->"Долгосрочные" to "Резервы по займам выданным долгосрочным"->"Долгосрочные";
DATACOPY "Прочие резервы"->"Краткосрочные" to "Резервы по займам выданным краткосрочным"->"Краткосрочные";

ENDFIX

/*
Статьи из формы 7.2
"Расчеты с бюджетом и внебюджетными фондами"
"КЗ перед бюджетом и внебюджетными фондами"

 */
FIX(
/*ПОКАЗАТЕЛИ 						*/
/*ПРОДУКЦИЯ 						*/"бу вида продукции"
/*НАПРАВЛЕНИЕ 	*/"бу направления"
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/"бу элемента затрат"
/*КОНТРАГЕНТЫ 						*/@RELATIVE("Всего по уровням бюджета",0)
/*ПРОЧЕЕ 							*/"бу прочих справочников"
/*СЧЕТА БАЛАНСА 					*/"68_ДЗ (+), КЗ (-) на конец"
/*ВИД ЗАТРАТ 						*/@RELATIVE("Налоги",0)
/*СТАТЬИ 							*/
)
FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
)
DATACOPY "Оплата налогов" to "Расчеты с бюджетом и внебюджетными фондами";
DATACOPY "Оплата налогов" to "КЗ перед бюджетом и внебюджетными фондами";

"Расчеты с бюджетом и внебюджетными фондами"=#missing;
"КЗ перед бюджетом и внебюджетными фондами"=#missing;
ENDFIX
FIX(
/*СТАТЬИ 							*/"Оплата налогов" 
)

"Сумма"(
IF(Сумма > 0)
Сумма->"Расчеты с бюджетом и внебюджетными фондами"= Сумма;
ELSE
Сумма->"КЗ перед бюджетом и внебюджетными фондами"= -Сумма;
ENDIF;
)

ENDFIX
ENDFIX


/*
Статьи из формы 7.3
НДС по приобретенным ценностям

 */
FIX(
/*ПОКАЗАТЕЛИ 						*/Сумма
/*ПРОДУКЦИЯ 						*/"бу вида продукции"
/*НАПРАВЛЕНИЕ 	*/"бу направления"
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/"бу элемента затрат"
/*КОНТРАГЕНТЫ 						*/"Прочие внешние"
/*ПРОЧЕЕ 							*/"бу прочих справочников"
/*СЧЕТА БАЛАНСА 					*/
/*ВИД ЗАТРАТ 						*/"НДС"
/*СТАТЬИ 							*/
)
FIX(
/*СЧЕТА БАЛАНСА 					*/"НДС по приобретенным ценностям (ост. на конец)"
)
DATACOPY "бу статьи" to "НДС по приобретенным ценностям";

ENDFIX
FIX(
/*СЧЕТА БАЛАНСА 					*/"НДС по авансам полученым (ост. на конец)"
)

DATACOPY "бу статьи" to "НДС по авансам полученным";
ENDFIX
ENDFIX

FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
/*ПРОДУКЦИЯ 						*/
/*НАПРАВЛЕНИЕ 	*/"бу направления"
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/"бу элемента затрат"
/*КОНТРАГЕНТЫ 						*/"Прочие внешние"
/*ПРОЧЕЕ 							*/"бу прочих справочников"
/*СЧЕТА БАЛАНСА 					*/"70_ДЗ (+), КЗ (-) на конец"
/*ВИД ЗАТРАТ 						*/"ФОТ.","Страховые взносы"
/*СТАТЬИ 							*/
)
FIX(
/*ПРОДУКЦИЯ 						*/"бу вида продукции"
)
DATACOPY "Зарплата" to "Расчеты с персоналом";
DATACOPY "НДФЛ" to "Расчеты с персоналом";
DATACOPY "Отчисления на соц страх-е" to "Расчеты с персоналом";

DATACOPY "Зарплата" to "КЗ перед персоналом";
DATACOPY "НДФЛ" to "КЗ перед персоналом";
DATACOPY "Отчисления на соц страх-е" to "КЗ перед персоналом";


"Расчеты с персоналом"=#missing;
"КЗ перед персоналом"=#missing;
ENDFIX

FIX(
/*СТАТЬИ 							*/"Зарплата","НДФЛ","Отчисления на соц страх-е"
)


"бу вида продукции"(
IF("бу вида продукции" > 0 ) 
"бу вида продукции"->"Расчеты с персоналом" = "бу вида продукции"-> "Расчеты с персоналом"+"бу вида продукции";
ELSEIF("бу вида продукции" < 0 )
"бу вида продукции"->"КЗ перед персоналом" = "бу вида продукции"->"КЗ перед персоналом" - "бу вида продукции";
ENDIF
)

ENDFIX
ENDFIX

/*
Статьи из формы 8.5.1 -2 
"РБП"

 */
FIX(
/*ПОКАЗАТЕЛИ 						*/"Сумма"
/*ПРОДУКЦИЯ 						*/"бу вида продукции"
/*НАПРАВЛЕНИЕ 	*/"бу направления"
/*ЭЛЕМЕНТЫ_ЗАТРАТ 					*/"бу элемента затрат"
/*КОНТРАГЕНТЫ 						*/"бу контрагента"
/*ПРОЧЕЕ 							*/"бу прочих справочников"
/*СЧЕТА БАЛАНСА 					*/"97_Остаток на конец"
/*ВИД ЗАТРАТ 						*/"бу видов затрат"
/*СТАТЬИ 							*/
)

DATACOPY "Прочие РБП" to "РБП";

ENDFIX 


ENDFIX
ENDFIX  
ENDFIX]]></script>
    </rule>
  </rules>
  <components></components>
</HBRRepo>
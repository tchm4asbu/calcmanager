<?xml version="1.0" encoding="UTF-8"?>
<HBRRepo>
  <variables>
    <variable id="1" name="ГОД" product="Planning" type="member" usage="const">
      <property name="array_type">string</property>
      <property name="dimensionInputMode">type</property>
      <property name="dimensionType">Year</property>
      <property name="prompt_text">Выберите Год</property>
      <property name="scope">ruleset</property>
      <value>FY18</value>
    </variable>
    <variable id="2" name="ВЕРСИЯ" product="Planning" type="member" usage="const">
      <property name="array_type">string</property>
      <property name="dimensionInputMode">type</property>
      <property name="dimensionType">Version</property>
      <property name="prompt_text">Выберите Версию</property>
      <property name="scope">ruleset</property>
      <value>Рабочая_1</value>
    </variable>
    <variable id="3" name="СЦЕНАРИЙ" product="Planning" type="member" usage="const">
      <property name="array_type">string</property>
      <property name="dimensionInputMode">type</property>
      <property name="dimensionType">Scenario</property>
      <property name="prompt_text">Выберите Сценарий</property>
      <property name="scope">ruleset</property>
      <value>Проект</value>
    </variable>
  </variables>
  <rulesets></rulesets>
  <rules>
    <rule id="1" name="2.10.09.Производcтвенная программа ТЭЦ-ПВС" product="Planning">
      <property name="description">Производcтвенная программа ТЭЦ-ПВС</property>
      <property name="application">ТЧМ</property>
      <property name="plantype">Plan1</property>
      <property name="display_label">2.10.09.Производcтвенная программа ТЭЦ-ПВС</property>
      <property name="enableNotifications">false</property>
      <property name="enablePostProcessing">false</property>
      <property name="enablePreProcessing">false</property>
      <variable_references>
        <variable_reference id="2" name="ВЕРСИЯ">
          <property name="hasvalue">true</property>
          <property name="hidden">false</property>
          <property name="rule_name">2.10.09.Производcтвенная программа ТЭЦ-ПВС</property>
          <property name="seq">1</property>
          <property name="type">3</property>
          <property name="useAsOverrideValue">false</property>
        </variable_reference>
        <variable_reference id="1" name="ГОД">
          <property name="hasvalue">true</property>
          <property name="hidden">false</property>
          <property name="rule_name">2.10.09.Производcтвенная программа ТЭЦ-ПВС</property>
          <property name="seq">2</property>
          <property name="type">3</property>
          <property name="useAsOverrideValue">false</property>
        </variable_reference>
        <variable_reference id="3" name="СЦЕНАРИЙ">
          <property name="hasvalue">true</property>
          <property name="hidden">false</property>
          <property name="rule_name">2.10.09.Производcтвенная программа ТЭЦ-ПВС</property>
          <property name="seq">3</property>
          <property name="type">3</property>
          <property name="useAsOverrideValue">false</property>
        </variable_reference>
      </variable_references>
      <script><![CDATA[SET AGGMISSG ON;
SET UPDATECALC OFF;
SET CREATEBLOCKONEQ OFF;
SET CREATENONMISSINGBLK OFF;
SET FRMLBOTTOMUP OFF;
SET REMOTECALC OFF;
SET CALCPARALLEL 1;
SET CALCTASKDIMS 1;
SET EMPTYMEMBERSETS ON;
SET FRMLRTDYNAMIC OFF;
SET MSG INFO;
/* Проверка корректности рассчитываемого среза данных */

FIX(
/* ВАЛЮТА							*/	"бу валюты"
/* ПЕРИОД							*/	"P0"
/* ГОД								*/	"No Year"
/* ВЕРСИЯ							*/	
/* СЦЕНАРИЙ							*/	
/* ЦФО								*/	"бу ЦФО"
/* МВЗ								*/	"бу МВЗ"
/* МВЗ ПОЛУЧАТЕЛЬ					*/	"бу МВЗ (пол-ль)"
/* СЧЕТА БАЛАНСА					*/	"80_Увеличение"
/* СТАТЬИ							*/	
/* ПОКАЗАТЕЛИ						*/	"Сумма"
/* ВИД ЗАТРАТ						*/	"бу видов затрат"
/* ПРОДУКЦИЯ						*/	"бу вида продукции"
/* КОНТРАГЕНТЫ						*/	"бу контрагента"
/* НАПРАВЛЕНИЕ	*/	"бу направления"
/* ЭЛЕМЕНТЫ_ЗАТРАТ					*/	"бу элемента затрат"
/* ПРОЧЕЕ							*/	"бу прочих справочников"

)
  SET CREATENONMISSINGBLK ON;

  
  var ErrFlagScenario=0;
  var ErrFlagVersion=0;
  var NextM=2;
  /* ПРОВЕРКА КОРРЕКТНОСТИ ВЫБОРА СЦЕНАРИЯ */
  FIX(/* Сценарий */ {СЦЕНАРИЙ} AND @LIST(&ОТКРЫТЫЕ_СЦЕНАРИИ))
    FIX(
    /* Сценарий */ 
    /* Версия   */ "Рабочая 1"
    )
     "бу статьи" (
     IF (ErrFlagScenario==0)
            ErrFlagScenario=1;
     ENDIF
     );

    ENDFIX
  ENDFIX

   /* ПРОВЕРКА КОРРЕКТНОСТИ ВЫБОРА ВЕРСИИ */
  FIX(/* Версия  */ {ВЕРСИЯ} AND @LIST(&ОТКРЫТЫЕ_ВЕРСИИ))
    FIX(
    /* Сценарий */ "Проект"
    /* Версия   */ 
    )
     "бу статьи" (
     IF (ErrFlagVersion==0)
            ErrFlagVersion=1;
     ENDIF
     );
    ENDFIX
  ENDFIX

  /* ВЫВОД СООБЩЕНИЙ О НЕКОРРЕКТНОМ СРЕЗЕ */
  FIX(
  /* Сценарий     */ "Проект"
  /* Версия       */ "Рабочая 1"
  )

   "бу статьи" (
    IF (ErrFlagScenario==0)
       @RETURN("Выбран сценарий, закрытый для расчета. Расчет не произведен.",ERROR);
    ENDIF
    IF (ErrFlagVersion==0)
       @RETURN("Выбрана версия, закрытая для расчета. Расчет не произведен.",ERROR);
    ENDIF
    )

   ENDFIX
ENDFIX
SET CREATENONMISSINGBLK OFF;
FIX(
/*ЦФО 								*/"ОГЭ"
/*ВАЛЮТА 							*/"RUR"
/*КОНТРАГЕНТЫ 						*/"бу контрагента"
/*МВЗ 								*/
/*НАПРАВЛЕНИЕ 						*/
/*ЭЛЕМЕНТЫ_ЗАТРАТ					*/"бу элемента затрат"
/*ГОД								*/{ГОД}
/*ВЕРСИЯ							*/{ВЕРСИЯ}
/*ПЕРИОД							*/@RELATIVE("Всего год",0)
/*СЦЕНАРИЙ							*/{СЦЕНАРИЙ}
/*ВИД ЗАТРАТ						*/"бу видов затрат"
/*ПРОЧЕЕ							*/"бу прочих справочников"
/*МВЗ ПОЛУЧАТЕЛЬ					*/
/*ПРОДУКЦИЯ							*/
/*СТАТЬИ							*/"бу статьи"
/*СЧЕТА БАЛАНСА						*/"20_Выбытие"
/*ПОКАЗАТЕЛИ						*/
)

SET CREATENONMISSINGBLK ON;

/*  "Кол-во для собственных нужд" для "Горячая вода ТЭЦ-ПВС." подтягивается из энергобаланса. Расчет на форме энергобаланса по статьям */



/*  Расчет "Кол-во продукции итого" по "Итого по направлениям" и "ТЧМ итого (пол-ль)"*/

FIX(
/*МВЗ 								*/@Relative("№16. ТЭЦ-ПВС",0)
/*НАПРАВЛЕНИЕ 						*/"бу направления"
/*МВЗ ПОЛУЧАТЕЛЬ					*/"ТЧМ в целом (пол-ль)"
/*ПРОДУКЦИЯ							*/"Обессоленная вода.","Дутье.","Химочищенная вода.","Сжатый воздух ТЭЦ-ПВС."
/*ПОКАЗАТЕЛИ						*/
)					
                            
"Кол-во продукции итого" = 
"Кол-во для собственных нужд"->"Цехи п-п (сч 20;23;26)"->"№16. ТЭЦ-ПВС (пол-ль)"
+"Кол-во для других цехов"->"Цехи п-п (сч 20;23;26)"->"Уч горячей воды (пол-ль)"
+"Кол-во для других цехов"->"Цехи п-п (сч 20;23;26)"->"Уч пара (пол-ль)"
+"Кол-во для других цехов"->"Цехи п-п (сч 20;23;26)"->"Уч передачи электроэнергии (пол-ль)"
+"Кол-во для других цехов"->"Цехи п-п (сч 20;23;26)"->"ОПР ДП (пол-ль)"
+"Кол-во для других цехов"->"Цехи п-п (сч 20;23;26)"->"Уч пр-ва сжатого воздуха (пол-ль)";

ENDFIX

FIX(
/*МВЗ 								*/"Уч пр-ва электроэнергии ТЭЦ-ПВС"
/*НАПРАВЛЕНИЕ					 	*/"бу направления"
/*МВЗ ПОЛУЧАТЕЛЬ					*/"ТЧМ в целом (пол-ль)"
/*ПРОДУКЦИЯ							*/"Электроэнергия ТЭЦ-ПВС."
/*ПОКАЗАТЕЛИ						*/
)	
                        
"Кол-во продукции итого"  = 
"Кол-во для собственных нужд"->"Цехи п-п (сч 20;23;26)"->"Прочие МВЗ (пол-ль)"
+"Кол-во"->"Электроэнергия"->"ТЧМ в целом"->"бу вида продукции"->"Реализация энергоресурсов"->"20_Начисление"->"бу МВЗ (пол-ль)"
+"Кол-во"->"Электроэнергия"->"ТЧМ итого"->"бу вида продукции"->"ЦЕХОВАЯ СЕБЕСТОИМОСТЬ"->"20_Начисление"->"бу МВЗ (пол-ль)"
+"Кол-во"->"Потери эл. энергии"->"Уч передачи электроэнергии"->"Электроэнергия с учетом передачи"->"ПЭО"->"Энергозатраты на технологию"->"20_Начисление"->"бу МВЗ (пол-ль)"
-"Закупка электроэнергии на время ремонта";


ENDFIX



/********** НАЧАЛО РАСЧЕТА ФОРМЫ НОРМАТИВОВ ТЕЦ ПВС *********/

FIX(
/*ЦФО 								*/"ОГЭ"
/*ВАЛЮТА 							*/"RUR"
/*КОНТРАГЕНТЫ 						*/"бу контрагента"
/*МВЗ 								*/
/*НАПРАВЛЕНИЕ 						*/"бу направления"
/*ЭЛЕМЕНТЫ_ЗАТРАТ					*/"бу элемента затрат"
/*ГОД								*/{ГОД}
/*ВЕРСИЯ							*/{ВЕРСИЯ}
/*ПЕРИОД							*/@RELATIVE("Всего год",0)
/*СЦЕНАРИЙ							*/{СЦЕНАРИЙ}
/*ВИД ЗАТРАТ						*/
/*ПРОЧЕЕ							*/"бу прочих справочников"
/*МВЗ ПОЛУЧАТЕЛЬ					*/"бу МВЗ (пол-ль)"
/*ПРОДУКЦИЯ							*/
/*СТАТЬИ							*/
/*СЧЕТА БАЛАНСА						*/"20_Начисление"
/*ПОКАЗАТЕЛИ						*/
)

/*  Расчитываем "Норма на ед." для "Пар ТЭЦ-ПВС" */

FIX(
/*МВЗ 								*/"Уч пр-ва электроэнергии ТЭЦ-ПВС","Уч пр-ва горячей воды ТЭЦ-ПВС","Уч обессоленной воды","Уч пр-ва дутья","Уч химочищенной воды","Уч пр-ва сжатого воздуха ТЭЦ-ПВС"
/*ВИД ЗАТРАТ						*/"Пар ТЭЦ-ПВС"
/*ПРОДУКЦИЯ							*/"Электроэнергия ТЭЦ-ПВС.","Горячая вода ТЭЦ-ПВС.","Обессоленная вода.","Дутье.","Химочищенная вода.","Сжатый воздух ТЭЦ-ПВС."
/*СТАТЬИ							*/"Энергозатраты на технологию"
/*ПОКАЗАТЕЛИ						*/
)
"Норма на ед."="Кол-во" / "Кол-во продукции итого"->"бу видов затрат"->"бу направления"->"бу статьи"->"20_Выбытие"->"ТЧМ в целом (пол-ль)";
/*"Кол-во" = "Норма на ед." *"Кол-во продукции итого"->"бу видов затрат"->"бу направления"->"бу статьи"->"20_Выбытие"->"ТЧМ в целом (пол-ль)";*/

ENDFIX






/*  Расчитываем "Кол-во" для "Электроэнергия ТЭЦ-ПВС" */

FIX(
/*МВЗ 								*/"Уч пр-ва электроэнергии ТЭЦ-ПВС","Уч пр-ва горячей воды ТЭЦ-ПВС","Уч обессоленной воды","Уч пр-ва дутья","Уч химочищенной воды","Уч пр-ва сжатого воздуха ТЭЦ-ПВС"
/*ВИД ЗАТРАТ						*/"Электроэнергия ТЭЦ-ПВС"
/*ПРОДУКЦИЯ							*/"Электроэнергия ТЭЦ-ПВС.","Горячая вода ТЭЦ-ПВС.","Обессоленная вода.","Дутье.","Химочищенная вода.","Сжатый воздух ТЭЦ-ПВС."
/*СТАТЬИ							*/"Энергозатраты на технологию"
/*ПОКАЗАТЕЛИ						*/
)

"Кол-во" = "Норма на ед." *"Кол-во продукции итого"->"бу видов затрат"->"бу направления"->"бу статьи"->"20_Выбытие"->"ТЧМ в целом (пол-ль)";


ENDFIX

FIX(
/*МВЗ 								*/"Уч пр-ва пара ТЭЦ-ПВС"
/*ВИД ЗАТРАТ						*/"Электроэнергия ТЭЦ-ПВС"
/*ПРОДУКЦИЯ							*/"Пар ТЭЦ-ПВС."
/*СТАТЬИ							*/"Энергозатраты на технологию"
/*ПОКАЗАТЕЛИ						*/
)


"Кол-во" = "Кол-во для собственных нужд"->"Уч пр-ва электроэнергии ТЭЦ-ПВС"->"бу видов затрат"->"Цехи п-п (сч 20;23;26)"->"Электроэнергия ТЭЦ-ПВС."->"бу статьи"->"20_Выбытие"->"Уч пр-ва пара ТЭЦ-ПВС (пол-ль)";

"Норма на ед." = "Кол-во" / "Кол-во продукции итого"->"бу видов затрат"->"бу направления"->"бу статьи"->"20_Выбытие"->"ТЧМ в целом (пол-ль)";

ENDFIX

/*  Расчитываем "Кол-во" для "Обессоленная вода" */

FIX(
/*МВЗ 								*/"Уч пр-ва электроэнергии ТЭЦ-ПВС","Уч пр-ва пара ТЭЦ-ПВС","Уч пр-ва горячей воды ТЭЦ-ПВС","Уч обессоленной воды","Уч пр-ва дутья","Уч химочищенной воды","Уч пр-ва сжатого воздуха ТЭЦ-ПВС"
/*ВИД ЗАТРАТ						*/"Обессоленная вода"
/*ПРОДУКЦИЯ							*/"Электроэнергия ТЭЦ-ПВС.","Пар ТЭЦ-ПВС.","Горячая вода ТЭЦ-ПВС.","Обессоленная вода.","Дутье.","Химочищенная вода.","Сжатый воздух ТЭЦ-ПВС."
/*СТАТЬИ							*/"Энергозатраты на технологию"
/*ПОКАЗАТЕЛИ						*/
)

"Кол-во" (
"Кол-во"=#missing;
IF( (@ISMBR("Уч пр-ва электроэнергии ТЭЦ-ПВС") AND @ISMBR("Электроэнергия ТЭЦ-ПВС.")) OR
	(@ISMBR("Уч пр-ва пара ТЭЦ-ПВС") AND @ISMBR("Пар ТЭЦ-ПВС.")) OR
	(@ISMBR("Уч пр-ва горячей воды ТЭЦ-ПВС") AND @ISMBR("Горячая вода ТЭЦ-ПВС.")) OR
    (@ISMBR("Уч обессоленной воды") AND @ISMBR("Обессоленная вода.")) OR
    (@ISMBR("Уч пр-ва дутья") AND @ISMBR("Дутье.")) OR
    (@ISMBR("Уч химочищенной воды") AND @ISMBR("Химочищенная вода.")) OR
    (@ISMBR("Уч пр-ва сжатого воздуха ТЭЦ-ПВС") AND @ISMBR("Сжатый воздух ТЭЦ-ПВС.")))
"Кол-во" = "Кол-во для собственных нужд"->"бу видов затрат"->"Уч обессоленной воды"->"Цехи п-п (сч 20;23;26)"->"Обессоленная вода."->"бу статьи"->"20_Выбытие"->@MEMBER(@CONCATENATE(@NAME(@CURRMBR("МВЗ"))," (пол-ль)"));
ENDIF
)


ENDFIX


/*  Расчитываем "Норма на ед." для "Техническая вода" */

FIX(
/*МВЗ 								*/"Уч пр-ва электроэнергии ТЭЦ-ПВС","Уч пр-ва пара ТЭЦ-ПВС","Уч пр-ва горячей воды ТЭЦ-ПВС","Уч обессоленной воды","Уч пр-ва дутья","Уч химочищенной воды","Уч пр-ва сжатого воздуха ТЭЦ-ПВС"
/*ВИД ЗАТРАТ						*/"Техническая вода"
/*ПРОДУКЦИЯ							*/"Электроэнергия ТЭЦ-ПВС.","Пар ТЭЦ-ПВС.","Горячая вода ТЭЦ-ПВС.","Обессоленная вода.","Дутье.","Химочищенная вода.","Сжатый воздух ТЭЦ-ПВС."
/*СТАТЬИ							*/"Энергозатраты на технологию"
/*ПОКАЗАТЕЛИ						*/
)

"Норма на ед."="Кол-во" / "Кол-во продукции итого"->"бу видов затрат"->"бу направления"->"бу статьи"->"20_Выбытие"->"ТЧМ в целом (пол-ль)";
/*"Кол-во" = "Норма на ед." * "Кол-во продукции итого"->"бу видов затрат"->"бу направления"->"бу статьи"->"20_Выбытие"->"ТЧМ в целом (пол-ль)";*/

ENDFIX

/*  Расчитываем "Кол-во" для "Химочищенная вода" */

FIX(
/*МВЗ 								*/"Уч пр-ва электроэнергии ТЭЦ-ПВС","Уч пр-ва пара ТЭЦ-ПВС","Уч пр-ва горячей воды ТЭЦ-ПВС","Уч обессоленной воды","Уч пр-ва дутья","Уч химочищенной воды","Уч пр-ва сжатого воздуха ТЭЦ-ПВС"
/*ВИД ЗАТРАТ						*/"Химочищенная вода" 
/*ПРОДУКЦИЯ							*/"Электроэнергия ТЭЦ-ПВС.","Пар ТЭЦ-ПВС.","Горячая вода ТЭЦ-ПВС.","Обессоленная вода.","Дутье.","Химочищенная вода.","Сжатый воздух ТЭЦ-ПВС."
/*СТАТЬИ							*/"Энергозатраты на технологию"
/*ПОКАЗАТЕЛИ						*/
)

"Кол-во" (
"Кол-во"=#missing;
IF( (@ISMBR("Уч пр-ва электроэнергии ТЭЦ-ПВС") AND @ISMBR("Электроэнергия ТЭЦ-ПВС.")) OR
	(@ISMBR("Уч пр-ва пара ТЭЦ-ПВС") AND @ISMBR("Пар ТЭЦ-ПВС.")) OR
	(@ISMBR("Уч пр-ва горячей воды ТЭЦ-ПВС") AND @ISMBR("Горячая вода ТЭЦ-ПВС.")) OR
    (@ISMBR("Уч обессоленной воды") AND @ISMBR("Обессоленная вода.")) OR
    (@ISMBR("Уч пр-ва дутья") AND @ISMBR("Дутье.")) OR
    (@ISMBR("Уч химочищенной воды") AND @ISMBR("Химочищенная вода.")) OR
    (@ISMBR("Уч пр-ва сжатого воздуха ТЭЦ-ПВС") AND @ISMBR("Сжатый воздух ТЭЦ-ПВС.")))
"Кол-во" = "Кол-во для собственных нужд"->"бу видов затрат"->"Уч химочищенной воды"->"Цехи п-п (сч 20;23;26)"->"Химочищенная вода."->"бу статьи"->"20_Выбытие"->@MEMBER(@CONCATENATE(@NAME(@CURRMBR("МВЗ"))," (пол-ль)"));
ENDIF
)

ENDFIX


/*  Расчитываем "Кол-во" для "Натр едкий технический","Кислота серная техническая","Соль техническая","Железный купорос","Известь со стороны"  */

FIX(
/*МВЗ 								*/"Уч пр-ва электроэнергии ТЭЦ-ПВС","Уч пр-ва пара ТЭЦ-ПВС","Уч пр-ва горячей воды ТЭЦ-ПВС","Уч обессоленной воды","Уч пр-ва дутья","Уч химочищенной воды","Уч пр-ва сжатого воздуха ТЭЦ-ПВС"
/*ВИД ЗАТРАТ						*/"Соль техническая","Железный купорос","Известь со стороны","Натр едкий технический","Кислота серная техническая"
/*ПРОДУКЦИЯ							*/"Электроэнергия ТЭЦ-ПВС.","Пар ТЭЦ-ПВС.","Горячая вода ТЭЦ-ПВС.","Обессоленная вода.","Дутье.","Химочищенная вода.","Сжатый воздух ТЭЦ-ПВС."
/*СТАТЬИ							*/"Сырье и основные материалы."
/*ПОКАЗАТЕЛИ						*/
)

"Кол-во" = "Норма на ед." * "Кол-во продукции итого"->"бу видов затрат"->"бу направления"->"бу статьи"->"20_Выбытие"->"ТЧМ в целом (пол-ль)";

ENDFIX

/*  Расчитываем "Норма на ед."  для вида затрат "Обессоленная вода","Химочищенная вода","Газ доменный очищенный","Газ природный с учетом транспортировки","Уголь" */

FIX(
/*МВЗ 								*/"Уч пр-ва электроэнергии ТЭЦ-ПВС","Уч пр-ва пара ТЭЦ-ПВС","Уч пр-ва горячей воды ТЭЦ-ПВС","Уч обессоленной воды","Уч пр-ва дутья","Уч химочищенной воды","Уч пр-ва сжатого воздуха ТЭЦ-ПВС"
/*ВИД ЗАТРАТ						*/"Обессоленная вода","Химочищенная вода"
/*ПРОДУКЦИЯ							*/"Электроэнергия ТЭЦ-ПВС.","Пар ТЭЦ-ПВС.","Горячая вода ТЭЦ-ПВС.","Обессоленная вода.","Дутье.","Химочищенная вода.","Сжатый воздух ТЭЦ-ПВС."
/*СТАТЬИ							*/"Энергозатраты на технологию"
/*ПОКАЗАТЕЛИ						*/
)

"Норма на ед." =  "Кол-во" /"Кол-во продукции итого"->"бу видов затрат"->"бу направления"->"бу статьи"->"20_Выбытие"->"ТЧМ в целом (пол-ль)";

ENDFIX
FIX(
/*МВЗ 								*/"Уч пр-ва электроэнергии ТЭЦ-ПВС","Уч пр-ва пара ТЭЦ-ПВС","Уч пр-ва горячей воды ТЭЦ-ПВС","Уч обессоленной воды","Уч пр-ва дутья","Уч химочищенной воды","Уч пр-ва сжатого воздуха ТЭЦ-ПВС"
/*ВИД ЗАТРАТ						*/"Газ доменный очищенный","Газ природный с учетом транспортировки","Уголь"
/*ПРОДУКЦИЯ							*/"Электроэнергия ТЭЦ-ПВС.","Пар ТЭЦ-ПВС.","Горячая вода ТЭЦ-ПВС.","Обессоленная вода.","Дутье.","Химочищенная вода.","Сжатый воздух ТЭЦ-ПВС."
/*СТАТЬИ							*/"Топливо технологическое."
/*ПОКАЗАТЕЛИ						*/
)

"Норма на ед." =  "Кол-во" /"Кол-во продукции итого"->"бу видов затрат"->"бу направления"->"бу статьи"->"20_Выбытие"->"ТЧМ в целом (пол-ль)";

ENDFIX




ENDFIX

/******************* ОКОНЧАНИЕ РАСЧЕТА ФОРМЫ НОРМАТИВОВ ТЕЦ ПВС ****************/



/*  "Кол-во для собственных нужд" для "Электроэнергия ТЭЦ-ПВС."->Уч пр-ва электроэнергии ТЭЦ-ПВС */

FIX(
/*МВЗ 								*/"Уч пр-ва электроэнергии ТЭЦ-ПВС"
/*НАПРАВЛЕНИЕ					 	*/"Цехи п-п (сч 20;23;26)"
/*МВЗ ПОЛУЧАТЕЛЬ					*/
/*ПРОДУКЦИЯ							*/"Электроэнергия ТЭЦ-ПВС."
/*ПОКАЗАТЕЛИ						*/"Кол-во для собственных нужд"
)

/*  "Кол-во для собственных нужд" для "Электроэнергия ТЭЦ-ПВС."->Уч пр-ва электроэнергии ТЭЦ-ПВС подтягиваем из НОРМАТИВОВ */

"ОПР ТЭЦ-ПВС (пол-ль)" = #missing;


"Уч обессоленной воды (пол-ль)" = "бу МВЗ (пол-ль)"-> "Электроэнергия ТЭЦ-ПВС"->"Уч обессоленной воды"->"бу направления"->"Обессоленная вода."->"Энергозатраты на технологию"->"20_Начисление"->"Кол-во";

"Уч пр-ва горячей воды ТЭЦ-ПВС (пол-ль)" = 	"бу МВЗ (пол-ль)"->"Электроэнергия ТЭЦ-ПВС"->"Уч пр-ва горячей воды ТЭЦ-ПВС"->"бу направления"->"Горячая вода ТЭЦ-ПВС."->"Энергозатраты на технологию"->"20_Начисление"->"Кол-во";

"Уч пр-ва дутья (пол-ль)" = "бу МВЗ (пол-ль)"->"Электроэнергия ТЭЦ-ПВС"->"Уч пр-ва дутья"->"бу направления"->"Дутье."->"Энергозатраты на технологию"->"20_Начисление"->"Кол-во";

"Уч пр-ва электроэнергии ТЭЦ-ПВС (пол-ль)" = "бу МВЗ (пол-ль)"->"Электроэнергия ТЭЦ-ПВС"->"Уч пр-ва электроэнергии ТЭЦ-ПВС"->"бу направления"->"Электроэнергия ТЭЦ-ПВС."->"Энергозатраты на технологию"->"20_Начисление"->"Кол-во";

"Уч пр-ва сжатого воздуха ТЭЦ-ПВС (пол-ль)" = "бу МВЗ (пол-ль)"->"Электроэнергия ТЭЦ-ПВС"->"Уч пр-ва сжатого воздуха ТЭЦ-ПВС"->"бу направления"->"Сжатый воздух ТЭЦ-ПВС."->"Энергозатраты на технологию"->"20_Начисление"->"Кол-во";


"Уч химочищенной воды (пол-ль)" = "бу МВЗ (пол-ль)"->"Электроэнергия ТЭЦ-ПВС"->"Уч химочищенной воды"->"бу направления"->"Химочищенная вода."->"Энергозатраты на технологию"->"20_Начисление"->"Кол-во";

/*  "Кол-во для собственных нужд" для "Электроэнергия ТЭЦ-ПВС." рассчитываем на форме */

"Уч пр-ва пара ТЭЦ-ПВС (пол-ль)"=  "Прочие МВЗ (пол-ль)" -"Уч обессоленной воды (пол-ль)"-"Уч пр-ва горячей воды ТЭЦ-ПВС (пол-ль)"-"Уч пр-ва дутья (пол-ль)" -"Уч пр-ва сжатого воздуха ТЭЦ-ПВС (пол-ль)"-"Уч пр-ва электроэнергии ТЭЦ-ПВС (пол-ль)"-"Уч химочищенной воды (пол-ль)";


ENDFIX

/*  "Кол-во для собственных нужд" для "Пар ТЭЦ-ПВС."->"Уч пр-ва пара ТЭЦ-ПВС" */

FIX(
/*МВЗ 								*/"Уч пр-ва пара ТЭЦ-ПВС"
/*НАПРАВЛЕНИЕ 						*/"Цехи п-п (сч 20;23;26)"
/*МВЗ ПОЛУЧАТЕЛЬ					*/
/*ПРОДУКЦИЯ							*/"Пар ТЭЦ-ПВС."
/*ПОКАЗАТЕЛИ						*/"Кол-во для собственных нужд"
)

/*  "Кол-во для собственных нужд" для "Пар ТЭЦ-ПВС." подтягиваем из НОРМАТИВОВ */

"ОПР ТЭЦ-ПВС (пол-ль)" = #missing;

"Уч обессоленной воды (пол-ль)" ="бу МВЗ (пол-ль)"-> "Пар ТЭЦ-ПВС"->"Уч обессоленной воды"->"бу направления"->"Обессоленная вода."->"Энергозатраты на технологию"->"20_Начисление"->"Кол-во";

"Уч пр-ва горячей воды ТЭЦ-ПВС (пол-ль)" = 	"бу МВЗ (пол-ль)"->"Пар ТЭЦ-ПВС"->"Уч пр-ва горячей воды ТЭЦ-ПВС"->"бу направления"->"Горячая вода ТЭЦ-ПВС."->"Энергозатраты на технологию"->"20_Начисление"->"Кол-во";

"Уч пр-ва дутья (пол-ль)" = "бу МВЗ (пол-ль)"->"Пар ТЭЦ-ПВС"->"Уч пр-ва дутья"->"бу направления"->"Дутье."->"Энергозатраты на технологию"->"20_Начисление"->"Кол-во";

"Уч пр-ва пара ТЭЦ-ПВС (пол-ль)" = "бу МВЗ (пол-ль)"->"Пар ТЭЦ-ПВС"->"Уч пр-ва пара ТЭЦ-ПВС"->"бу направления"->"Пар ТЭЦ-ПВС."->"Энергозатраты на технологию"->"20_Начисление"->"Кол-во";

"Уч химочищенной воды (пол-ль)" = "бу МВЗ (пол-ль)"->"Пар ТЭЦ-ПВС"->"Уч химочищенной воды"->"бу направления"->"Химочищенная вода."->"Энергозатраты на технологию"->"20_Начисление"->"Кол-во";

"Уч пр-ва электроэнергии ТЭЦ-ПВС (пол-ль)" = "бу МВЗ (пол-ль)"->"Пар ТЭЦ-ПВС"->"Уч пр-ва электроэнергии ТЭЦ-ПВС"->"бу направления"->"Электроэнергия ТЭЦ-ПВС."->"Энергозатраты на технологию"->"20_Начисление"->"Кол-во";

"Уч пр-ва сжатого воздуха ТЭЦ-ПВС (пол-ль)" = "бу МВЗ (пол-ль)"->"Пар ТЭЦ-ПВС"->"Уч пр-ва сжатого воздуха ТЭЦ-ПВС"->"бу направления"->"Сжатый воздух ТЭЦ-ПВС."->"Энергозатраты на технологию"->"20_Начисление"->"Кол-во";


ENDFIX

/*  Расчет "Кол-во продукции итого" по "бу напр-я списания" и "бу МВЗ"*/



FIX(
/*МВЗ 								*/"Уч пр-ва пара ТЭЦ-ПВС"
/*НАПРАВЛЕНИЕ 						*/"бу направления"
/*МВЗ ПОЛУЧАТЕЛЬ					*/"ТЧМ в целом (пол-ль)"
/*ПРОДУКЦИЯ							*/"Пар ТЭЦ-ПВС."
/*ПОКАЗАТЕЛИ						*/
)					
   
"Кол-во продукции итого"  = 
"Кол-во для собственных нужд"->"Цехи п-п (сч 20;23;26)"->"№16. ТЭЦ-ПВС (пол-ль)"
+"Кол-во"->"Пар"->"ТЧМ в целом"->"бу вида продукции"->"Реализация энергоресурсов"->"20_Начисление"->"бу МВЗ (пол-ль)"
+"Кол-во"->"Пар"->"ТЧМ итого"->"бу вида продукции"->"ЦЕХОВАЯ СЕБЕСТОИМОСТЬ"->"20_Начисление"->"бу МВЗ (пол-ль)"
+"Кол-во"->"Потери пара и горячей воды"->"Уч пара"->"Пар с учетом передачи"->"Энергозатраты на технологию"->"20_Начисление"->"бу МВЗ (пол-ль)";

ENDFIX

FIX(
/*МВЗ 								*/"Уч пр-ва горячей воды ТЭЦ-ПВС"
/*НАПРАВЛЕНИЕ 						*/"бу направления"
/*МВЗ ПОЛУЧАТЕЛЬ					*/"ТЧМ в целом (пол-ль)"
/*ПРОДУКЦИЯ							*/"Горячая вода ТЭЦ-ПВС."
/*ПОКАЗАТЕЛИ						*/
)					
                            
"Кол-во продукции итого"  = "Кол-во для собственных нужд"->"Цехи п-п (сч 20;23;26)"->"№16. ТЭЦ-ПВС (пол-ль)"
+"Кол-во"->"Горячая вода"->"ТЧМ в целом"->"бу вида продукции"->"Реализация энергоресурсов"->"20_Начисление"->"бу МВЗ (пол-ль)"
+"Кол-во"->"Горячая вода"->"ТЧМ итого"->"бу вида продукции"->"ЦЕХОВАЯ СЕБЕСТОИМОСТЬ"->"20_Начисление"->"бу МВЗ (пол-ль)"
+"Потери пара и горячей воды"->"Уч горячей воды"->"Горячая вода с учетом передачи"->"бу элемента затрат"->"Энергозатраты на технологию"->"20_Начисление"->"Кол-во"->"бу МВЗ (пол-ль)";
ENDFIX



/*  Расчет "Кол-во для других цехов" по "Цехи п-п (сч 20;23;26)" и "бу МВЗ"*/


FIX(
/*МВЗ 								*/"Уч пр-ва горячей воды ТЭЦ-ПВС","Уч пр-ва пара ТЭЦ-ПВС","Уч пр-ва электроэнергии ТЭЦ-ПВС"
/*НАПРАВЛЕНИЕ 						*/"Цехи п-п (сч 20;23;26)"
/*МВЗ ПОЛУЧАТЕЛЬ					*/"Уч горячей воды (пол-ль)", "Уч пара (пол-ль)" ,"Уч передачи электроэнергии (пол-ль)"
/*ПРОДУКЦИЯ							*/"Горячая вода ТЭЦ-ПВС.","Пар ТЭЦ-ПВС.","Электроэнергия ТЭЦ-ПВС."
/*ПОКАЗАТЕЛИ						*/
)					



"Кол-во для других цехов"(
IF((@ISMBR("Уч горячей воды (пол-ль)") AND @ISMBR("Горячая вода ТЭЦ-ПВС.")) OR
	(@ISMBR("Уч пара (пол-ль)") AND @ISMBR("Пар ТЭЦ-ПВС.")) OR
    (@ISMBR("Уч передачи электроэнергии (пол-ль)") AND @ISMBR("Электроэнергия ТЭЦ-ПВС.")) )
"Кол-во для других цехов"= "Кол-во продукции итого"->"ТЧМ в целом (пол-ль)"->"бу направления"- "Кол-во для собственных нужд"->"Цехи п-п (сч 20;23;26)"->"№16. ТЭЦ-ПВС (пол-ль)";
ENDIF
)



ENDFIX


/*  Расчет "Кол-во продукции итого" по "Итого по направлениям" и "ТЧМ итого (пол-ль)"*/

FIX(
/*МВЗ 								*/@Relative("№16. ТЭЦ-ПВС",0)
/*НАПРАВЛЕНИЕ 						*/"Цехи п-п (сч 20;23;26)"
/*МВЗ ПОЛУЧАТЕЛЬ					*/"ТЧМ в целом (пол-ль)"
/*ПРОДУКЦИЯ							*/"Электроэнергия ТЭЦ-ПВС.","Пар ТЭЦ-ПВС.","Горячая вода ТЭЦ-ПВС.","Обессоленная вода.","Дутье.","Химочищенная вода.","Сжатый воздух ТЭЦ-ПВС."
/*ПОКАЗАТЕЛИ						*/
)					
                            
"Кол-во продукции итого" = 
"Кол-во для собственных нужд"->"№16. ТЭЦ-ПВС (пол-ль)"
+"Кол-во для других цехов"->"Уч горячей воды (пол-ль)"
+"Кол-во для других цехов"->"Уч пара (пол-ль)"
+"Кол-во для других цехов"->"Уч передачи электроэнергии (пол-ль)"
+"Кол-во для других цехов"->"ОПР ДП (пол-ль)"
+"Кол-во для других цехов"->"Уч пр-ва сжатого воздуха (пол-ль)";

ENDFIX

FIX(
/*МВЗ 								*/@Relative("№16. ТЭЦ-ПВС",0)
/*НАПРАВЛЕНИЕ 						*/@Relative("Всего по направлениям списания", 0)
/*МВЗ ПОЛУЧАТЕЛЬ					*/ @Relative("№16. ТЭЦ-ПВС (пол-ль)", 0), @Relative("№17. Энергоцех (пол-ль)",0), @Relative("№19. Электроцех (пол-ль)",0)
/*ПРОДУКЦИЯ							*/"Электроэнергия ТЭЦ-ПВС.","Пар ТЭЦ-ПВС.","Горячая вода ТЭЦ-ПВС.","Обессоленная вода.","Дутье.","Химочищенная вода.","Сжатый воздух ТЭЦ-ПВС."
/*ПОКАЗАТЕЛИ						*/
)		

"Кол-во с учетом годного выхода" = "Кол-во для собственных нужд"+ "Кол-во для других цехов"+"Кол-во по портфелю"->"Тов. пр-ция (сч 43)"->"ТЧМ в целом (пол-ль)"  ;



ENDFIX


SET CREATENONMISSINGBLK OFF;
CLEARBLOCK EMPTY;

ENDFIX]]></script>
    </rule>
  </rules>
  <components></components>
</HBRRepo>
<?xml version="1.0" encoding="UTF-8"?>
<HBRRepo>
  <variables>
    <variable id="1" name="ГОД" product="Planning" type="member" usage="const">
      <property name="array_type">string</property>
      <property name="dimensionInputMode">type</property>
      <property name="dimensionType">Year</property>
      <property name="prompt_text">Выберите Год</property>
      <property name="scope">ruleset</property>
      <value>FY18</value>
    </variable>
    <variable id="2" name="ВЕРСИЯ" product="Planning" type="member" usage="const">
      <property name="array_type">string</property>
      <property name="dimensionInputMode">type</property>
      <property name="dimensionType">Version</property>
      <property name="prompt_text">Выберите Версию</property>
      <property name="scope">ruleset</property>
      <value>Рабочая_1</value>
    </variable>
    <variable id="3" name="СЦЕНАРИЙ" product="Planning" type="member" usage="const">
      <property name="array_type">string</property>
      <property name="dimensionInputMode">type</property>
      <property name="dimensionType">Scenario</property>
      <property name="prompt_text">Выберите Сценарий</property>
      <property name="scope">ruleset</property>
      <value>Проект</value>
    </variable>
  </variables>
  <rulesets></rulesets>
  <rules>
    <rule id="1" name="2.5.05.Производственная программа ЖДЦ" product="Planning">
      <property name="description">Программа перевозок в т и т на км</property>
      <property name="application">ТЧМ</property>
      <property name="plantype">Plan1</property>
      <property name="display_label">2.5.05.Производственная программа ЖДЦ</property>
      <property name="enablePostProcessing">false</property>
      <property name="enablePreProcessing">false</property>
      <variable_references>
        <variable_reference id="2" name="ВЕРСИЯ">
          <property name="hasvalue">true</property>
          <property name="hidden">false</property>
          <property name="rule_name">2.5.05.Производственная программа ЖДЦ</property>
          <property name="seq">1</property>
          <property name="type">3</property>
          <property name="useAsOverrideValue">false</property>
        </variable_reference>
        <variable_reference id="1" name="ГОД">
          <property name="hasvalue">true</property>
          <property name="hidden">false</property>
          <property name="rule_name">2.5.05.Производственная программа ЖДЦ</property>
          <property name="seq">2</property>
          <property name="type">3</property>
          <property name="useAsOverrideValue">false</property>
        </variable_reference>
        <variable_reference id="3" name="СЦЕНАРИЙ">
          <property name="hasvalue">true</property>
          <property name="hidden">false</property>
          <property name="rule_name">2.5.05.Производственная программа ЖДЦ</property>
          <property name="seq">3</property>
          <property name="type">3</property>
          <property name="useAsOverrideValue">false</property>
        </variable_reference>
      </variable_references>
      <script><![CDATA[SET AGGMISSG ON;
SET UPDATECALC OFF;
SET CREATEBLOCKONEQ OFF;
SET CREATENONMISSINGBLK OFF;
SET FRMLBOTTOMUP OFF;
SET REMOTECALC OFF;
SET CALCPARALLEL 1;
SET CALCTASKDIMS 1;
SET EMPTYMEMBERSETS ON;
SET FRMLRTDYNAMIC OFF;
SET MSG INFO;
/* Проверка корректности рассчитываемого среза данных */

FIX(
/* ВАЛЮТА							*/	"бу валюты"
/* ПЕРИОД							*/	"P0"
/* ГОД								*/	"No Year"
/* ВЕРСИЯ							*/	
/* СЦЕНАРИЙ							*/	
/* ЦФО								*/	"бу ЦФО"
/* МВЗ								*/	"бу МВЗ"
/* МВЗ ПОЛУЧАТЕЛЬ					*/	"бу МВЗ (пол-ль)"
/* СЧЕТА БАЛАНСА					*/	"80_Увеличение"
/* СТАТЬИ							*/	
/* ПОКАЗАТЕЛИ						*/	"Сумма"
/* ВИД ЗАТРАТ						*/	"бу видов затрат"
/* ПРОДУКЦИЯ						*/	"бу вида продукции"
/* КОНТРАГЕНТЫ						*/	"бу контрагента"
/* НАПРАВЛЕНИЕ	*/	"бу направления"
/* ЭЛЕМЕНТЫ_ЗАТРАТ					*/	"бу элемента затрат"
/* ПРОЧЕЕ							*/	"бу прочих справочников"

)
  SET CREATENONMISSINGBLK ON;

  
  var ErrFlagScenario=0;
  var ErrFlagVersion=0;
  var NextM=2;
  /* ПРОВЕРКА КОРРЕКТНОСТИ ВЫБОРА СЦЕНАРИЯ */
  FIX(/* Сценарий */ {СЦЕНАРИЙ} AND @LIST(&ОТКРЫТЫЕ_СЦЕНАРИИ))
    FIX(
    /* Сценарий */ 
    /* Версия   */ "Рабочая 1"
    )
     "бу статьи" (
     IF (ErrFlagScenario==0)
            ErrFlagScenario=1;
     ENDIF
     );

    ENDFIX
  ENDFIX

   /* ПРОВЕРКА КОРРЕКТНОСТИ ВЫБОРА ВЕРСИИ */
  FIX(/* Версия  */ {ВЕРСИЯ} AND @LIST(&ОТКРЫТЫЕ_ВЕРСИИ))
    FIX(
    /* Сценарий */ "Проект"
    /* Версия   */ 
    )
     "бу статьи" (
     IF (ErrFlagVersion==0)
            ErrFlagVersion=1;
     ENDIF
     );
    ENDFIX
  ENDFIX

  /* ВЫВОД СООБЩЕНИЙ О НЕКОРРЕКТНОМ СРЕЗЕ */
  FIX(
  /* Сценарий     */ "Проект"
  /* Версия       */ "Рабочая 1"
  )

   "бу статьи" (
    IF (ErrFlagScenario==0)
       @RETURN("Выбран сценарий, закрытый для расчета. Расчет не произведен.",ERROR);
    ENDIF
    IF (ErrFlagVersion==0)
       @RETURN("Выбрана версия, закрытая для расчета. Расчет не произведен.",ERROR);
    ENDIF
    )

   ENDFIX
ENDFIX
SET CREATENONMISSINGBLK OFF;
/*Агрегируем контрагентов 2.5.2,2.5.3,2.5.4 для переноса*/
FIX(
/*СТАТЬИ */"бу статьи"
/*ЦФО */"ПЭО","Уч развития подъездных путей"
/*МВЗ */@RELATIVE("№24. ЖДЦ",0),"Грузоперевозки_"
/*ВАЛЮТА */"RUR"
/*КОНТРАГЕНТЫ */
/*НАПРАВЛЕНИЕ */"бу направления",@RELATIVE("Всего по направлениям списания",0)
/*ЭЛЕМЕНТЫ_ЗАТРАТ */"бу элемента затрат"
/*СЧЕТА БАЛАНСА */"20_Выбытие"
/*ГОД */{ГОД}
/*ВЕРСИЯ */{ВЕРСИЯ}
/*ПЕРИОД */@RELATIVE("Всего год",0)
/*СЦЕНАРИЙ */{СЦЕНАРИЙ}
/*ПРОДУКЦИЯ */"бу вида продукции",@RELATIVE("Всего по продукции",0)
/*ВИД ЗАТРАТ */"бу видов затрат", @RELATIVE("Итого по видам затрат",0)
/*ПРОЧЕЕ */@RELATIVE("Всего по видам перевозок",0),"бу прочих справочников"
/*ПОКАЗАТЕЛИ */"Кол-во вагонов на провеску","Кол-во"
/*МВЗ ПОЛУЧАТЕЛЬ */@RELATIVE("ТЧМ итого (пол-ль)",0)
)

AGG("КОНТРАГЕНТЫ");
/*Контрагенты ТЧМ и ТЧМС*/

ENDFIX

/*Агрегируем продукцию 2.5.1 и 2.5.4 для переноса*/
FIX(
/*СТАТЬИ */"бу статьи"
/*ЦФО */"ПЭО","Уч развития подъездных путей"
/*МВЗ */@RELATIVE("№24. ЖДЦ",0),"Грузоперевозки_" 
/*ВАЛЮТА */"RUR"
/*КОНТРАГЕНТЫ */"Контрагенты ТЧМ и ТЧМС"
/*НАПРАВЛЕНИЕ */"бу направления"
/*ЭЛЕМЕНТЫ_ЗАТРАТ */"бу элемента затрат"
/*СЧЕТА БАЛАНСА */"20_Выбытие"
/*ГОД */{ГОД}
/*ВЕРСИЯ */{ВЕРСИЯ}
/*ПЕРИОД */@RELATIVE("Всего год",0)
/*СЦЕНАРИЙ */{СЦЕНАРИЙ}
/*ПРОДУКЦИЯ */
/*ВИД ЗАТРАТ */"бу видов затрат", @RELATIVE("Итого по видам затрат",0)
/*ПРОЧЕЕ */@RELATIVE("Всего по видам перевозок",0),"бу прочих справочников"
/*ПОКАЗАТЕЛИ */"Кол-во вагонов на провеску","Кол-во"
/*МВЗ ПОЛУЧАТЕЛЬ */@RELATIVE("ТЧМ итого (пол-ль)",0)
)

AGG("ПРОДУКЦИЯ");
/*"ПРОДУКЦИЯ ТЧМ и ТЧМС"*/

ENDFIX


/*Агрегируем виды затрат 2.5.1  для переноса*/
FIX(
/*СТАТЬИ */"бу статьи"
/*ЦФО */"ПЭО"
/*МВЗ */"Грузоперевозки_" 
/*ВАЛЮТА */"RUR"
/*КОНТРАГЕНТЫ */"Контрагенты ТЧМ и ТЧМС"
/*НАПРАВЛЕНИЕ */"бу направления"
/*ЭЛЕМЕНТЫ_ЗАТРАТ */"бу элемента затрат"
/*СЧЕТА БАЛАНСА */"20_Выбытие"
/*ГОД */{ГОД}
/*ВЕРСИЯ */{ВЕРСИЯ}
/*ПЕРИОД */@RELATIVE("Всего год",0)
/*СЦЕНАРИЙ */{СЦЕНАРИЙ}
/*ПРОДУКЦИЯ */"ПРОДУКЦИЯ ТЧМ и ТЧМС"
/*ВИД ЗАТРАТ */
/*ПРОЧЕЕ */@RELATIVE("Всего по видам перевозок",0),"бу прочих справочников"
/*ПОКАЗАТЕЛИ */"Кол-во вагонов на провеску","Кол-во"
/*МВЗ ПОЛУЧАТЕЛЬ */@RELATIVE("ТЧМ итого (пол-ль)",0)
)

AGG("ВИД ЗАТРАТ");
/*"ВИДЫ ЗАТРАТ ТЧМ И ТЧМС"*/

ENDFIX
FIX(
/*СТАТЬИ */"бу статьи"
/*ЦФО */
/*ВАЛЮТА */"RUR"
/*КОНТРАГЕНТЫ */"бу контрагента"
/*МВЗ */
/*НАПРАВЛЕНИЕ */
/*ЭЛЕМЕНТЫ_ЗАТРАТ */"бу элемента затрат"
/*СЧЕТА БАЛАНСА */"20_Выбытие"
/*ГОД */{ГОД}
/*ВЕРСИЯ */{ВЕРСИЯ}
/*ПЕРИОД */@RELATIVE("Всего год",0)
/*СЦЕНАРИЙ */{СЦЕНАРИЙ}
/*ПРОДУКЦИЯ */
/*ВИД ЗАТРАТ */"бу видов затрат"
/*ПРОЧЕЕ*/"бу прочих справочников"
/*ПОКАЗАТЕЛИ */
/*МВЗ ПОЛУЧАТЕЛЬ */
)

FIX(
/*МВЗ ПОЛУЧАТЕЛЬ 				*/"ТЧМ в целом (пол-ль)"
/*ЦФО */"ПЭО"
)

SET CREATENONMISSINGBLK ON;


	/*1. "Тов. пр-ция (сч 43)"*/
	FIX(
	/*НАПРАВЛЕНИЕ */"Тов. пр-ция (сч 43)"
	)

		FIX(									/*из 2.5.6*/
		/*ПРОДУКЦИЯ */"Работа локомотива."
		/*МВЗ */"Работа локомотива_"
		)
			"Кол-во с учетом годного выхода"=
			"Кол-во"->"Всего по контрагентам"->"Реализация продукции вспомогательных цехов"->"62_Реализация без НДС"->"ОПР ЖДЦ"->"бу направления";
			
		ENDFIX

		FIX(									/*из 2.5.2*/
		/*ПРОДУКЦИЯ */"Раскредитование грузов","Осмотр вагонов","Выдача копий документов"
		/*МВЗ */"Работа локомотива_"
		)
			"Кол-во с учетом годного выхода"=
			"Кол-во"->"Контрагенты ТЧМ и ТЧМС"/*временный агрегат*/->"ОПР ЖДЦ"->"бу направления";
			
		ENDFIX

		FIX(									/*из 2.5.2*/
		/*ПРОДУКЦИЯ */"Грузоперевозки."
		/*МВЗ */"Грузоперевозки_"
		)
			"Кол-во с учетом годного выхода"=
			"Кол-во"->"Поступление грузов контраг."->"Контрагенты ТЧМ и ТЧМС"->"ОПР ЖДЦ"->"бу направления"
			+"Кол-во"->"Отгрузка контраг."->"Контрагенты ТЧМ и ТЧМС"->"ОПР ЖДЦ"->"бу направления";
			
		ENDFIX
		
		FIX(									/*из 2.5.4*/
		/*ПРОДУКЦИЯ */"Провеска."
		/*МВЗ */"Провеска груза"
		)
			"Кол-во с учетом годного выхода"=
		@SUMRANGE("Кол-во вагонов на провеску"->"Грузоперевозки."->"Поступление грузов контраг."->"ОПР ЖДЦ"->"бу направления",@RELATIVE("Итого по контрагентам",0))
			+@SUMRANGE("Кол-во вагонов на провеску"->"Отгрузка контраг."->"Грузоперевозки."->"ОПР ЖДЦ"->"бу направления",@RELATIVE("Итого по контрагентам",0));
			
		ENDFIX
		
		FIX(									/*из 2.5.3*/
		/*ПРОДУКЦИЯ */"СМР для ЛПК"
		/*МВЗ */"УРПП"
		)
			"Кол-во с учетом годного выхода"
			="Кол-во"->"Уч развития подъездных путей"->"Контрагенты ТЧМ и ТЧМС"/*временный агрегат*/;
			
		ENDFIX
	ENDFIX


	/*2. "Кап.стр-во" - СМР для ЛПК*/
	FIX(
	/*НАПРАВЛЕНИЕ */"Кап стр-ва (сч 08)"
	)

		FIX(									/*из 2.5.3*/
		/*ПРОДУКЦИЯ */"СМР для ЛПК"
		/*МВЗ */"УРПП"
		)
			"Кол-во с учетом годного выхода"
			="Кол-во"->"Уч развития подъездных путей";
			
		ENDFIX

	ENDFIX

	/*3. "бу направления списания" - СМР для ЛПК */
	FIX(
	/*НАПРАВЛЕНИЕ */"бу напр-я списания"
    "20_НЗП на конец"
	)

		FIX(									/*из 2.5.3*/
		/*ПРОДУКЦИЯ */"СМР для ЛПК"
		/*МВЗ */"УРПП"
		)
			"Кол-во с учетом годного выхода"(
            IF(@ISMBR("20_НЗП на конец"))
			"Кол-во"->"20_Начисление"->"Уч развития подъездных путей"->"бу направления"
			- 
			("Кол-во"->"20_Выбытие"->"Цехи п-п (сч 20;23;26)"->"ТЧМ итого (пол-ль)"->"бу контрагента"->"Уч развития подъездных путей"
			+"Кол-во"->"20_Выбытие"->"Тов. пр-ция (сч 43)"->"ТЧМ в целом (пол-ль)"->"Контрагенты ТЧМ и ТЧМС"/*временный агрегат*/->"Уч развития подъездных путей"
			+"Кол-во"->"20_Выбытие"->"Кап стр-ва (сч 08)"->"ТЧМ в целом (пол-ль)"->"бу контрагента"->"Уч развития подъездных путей");
			ENDIF)
		ENDFIX

	ENDFIX
	SET CREATENONMISSINGBLK OFF;
	CLEARBLOCK EMPTY;

ENDFIX/*мвз получатель*/

/*4.1. Цехи п-п (сч 20;23;26)*/
FIX(
/*НАПРАВЛЕНИЕ */"Цехи п-п (сч 20;23;26)"
/*ЦФО */"ПЭО"
)

	SET CREATENONMISSINGBLK ON;
	
		FIX(									/*c текущей формы и 2.5.05.01*/
		/*ПРОДУКЦИЯ 	*/"Работа локомотива."	
		/*МВЗ			*/"Работа локомотива_"
		/*МВЗ ПОЛУЧАТЕЛЬ */"Провеска груза (пол-ль)"
		)
			"Кол-во с учетом годного выхода"=
			"Кол-во минут на провеску"->"ОПР ЖДЦ"->"бу направления"->"бу вида продукции"->"бу счета баланса"->"бу МВЗ (пол-ль)" / 60 
			* 
			("Провеска."->"Тов. пр-ция (сч 43)"->"ТЧМ в целом (пол-ль)"->"Провеска груза"
			+"Провеска."->"Цехи п-п (сч 20;23;26)"->"ТЧМ итого (пол-ль)"->"Провеска груза");
			
		ENDFIX
		
		FIX(									/*c текущей формы и 2.5.05.01*/
		/*ПРОДУКЦИЯ 	*/"Работа локомотива."	
		/*МВЗ			*/"Работа локомотива_"
		/*МВЗ ПОЛУЧАТЕЛЬ */"Грузоперевозки_ (пол-ль)"
		)
			"Кол-во с учетом годного выхода"=
			
			"Локомотиво часы всего"->"ОПР ЖДЦ"->"бу направления"->"бу вида продукции"->"бу счета баланса"->"бу МВЗ (пол-ль)"
			- "Кол-во с учетом годного выхода"->"Тов. пр-ция (сч 43)"->"ТЧМ в целом (пол-ль)"
			- "Кол-во с учетом годного выхода"->"Провеска груза (пол-ль)";
			
		ENDFIX
		
		FIX(
		/*ПРОДУКЦИЯ 	*/"Работа крана."	
		/*МВЗ			*/"Работа кранов"
		)
			FIX(									/*из 2.5.05.01*/
			/*МВЗ ПОЛУЧАТЕЛЬ */"Копровый Уч (пол-ль)"
			)
				"Кол-во с учетом годного выхода"=
				
				"Количество дней в месяце"->"ОПР ЖДЦ"->"бу направления"->"бу вида продукции"->"бу счета баланса"->"бу МВЗ (пол-ль)"
				* "Кол-во кранов на обслуживание"->"ОПР ЖДЦ"->"бу направления"->"бу вида продукции"->"бу счета баланса"->"бу МВЗ (пол-ль)"
				* "Время работы крана, час"->"ОПР ЖДЦ"->"бу направления"->"бу вида продукции"->"бу счета баланса"->"бу МВЗ (пол-ль)";
				
			ENDFIX
			
			FIX(									/*c текущей формы и 2.5.05.01*/
			/*МВЗ ПОЛУЧАТЕЛЬ */"Грузоперевозки_ (пол-ль)"
			)
				"Кол-во с учетом годного выхода"=
				
				("Количество дней в месяце"->"ОПР ЖДЦ"->"бу направления"->"бу вида продукции"->"бу счета баланса"->"бу МВЗ (пол-ль)"
				* "Кол-во кранов (всего)"->"ОПР ЖДЦ"->"бу направления"->"бу вида продукции"->"бу счета баланса"->"бу МВЗ (пол-ль)"
				* "Время работы крана, час"->"ОПР ЖДЦ"->"бу направления"->"бу вида продукции"->"бу счета баланса"->"бу МВЗ (пол-ль)")
				-
				"Кол-во с учетом годного выхода"->"Копровый Уч (пол-ль)";
				
			ENDFIX
		ENDFIX
		
	SET CREATENONMISSINGBLK OFF;
	CLEARBLOCK EMPTY;
	
ENDFIX/*направления списания отгрузки*/


FIX(
/*МВЗ ПОЛУЧАТЕЛЬ */@RELATIVE("ТЧМ итого (пол-ль)",0)
/*ЦФО */"ПЭО"
)
	
	SET CREATEBLOCKONEQ ON;
	
		/*4.2. Цехи п-п (сч 20;23;26)*/

		/*НАПРАВЛЕНИЕ *//*"Цехи п-п (сч 20;23;26)"*/
		FIX(									/*из 2.5.04*/
		/*ПРОДУКЦИЯ 	*/"Провеска."	
		/*МВЗ			*/"Провеска груза"
		/*ПОКАЗАТЕЛИ 	*/"Кол-во с учетом годного выхода"
		)
				
			"Цехи п-п (сч 20;23;26)"(
			"бу направления"->"Кол-во вагонов на провеску"->"Внутризаводские перевозки"->"бу вида продукции"->"ОПР ЖДЦ"
			+"бу направления"->"Кол-во вагонов на провеску"->"Отгрузка ТЧМ"->"Всего по продукции"/*временный агрегат*/->"ОПР ЖДЦ"
			+"бу направления"->"Кол-во вагонов на провеску"->"Поступление грузов ТЧМ"->"бу вида продукции"->"ОПР ЖДЦ";
            
			)
		ENDFIX

		/*НАПРАВЛЕНИЕ *//*"Цехи п-п (сч 20;23;26)"*/		
		FIX(									/*из 2.5.01*/
		/*ПРОДУКЦИЯ 	*/"Грузоперевозки."	
		/*МВЗ			*/"Грузоперевозки_"
		/*ПОКАЗАТЕЛИ 	*/"Кол-во с учетом годного выхода"
			)
				
				"Цехи п-п (сч 20;23;26)"(
				"бу направления"->"Кол-во"->"Поступление грузов ТЧМ"->"ВИДЫ ЗАТРАТ ТЧМ И ТЧМС"->"бу вида продукции"
				+"бу направления"->"Кол-во"->"Внутризаводские перевозки"->"ВИДЫ ЗАТРАТ ТЧМ И ТЧМС"->"ПРОДУКЦИЯ ТЧМ и ТЧМС"/*временный агрегат*/;
                

				)
		ENDFIX
		
		/*4. Расходы на продажу*/
		/*НАПРАВЛЕНИЕ *//*"Расходы на продажу (сч 44)"*/
		FIX(									/*из 2.5.04*/
		/*ПРОДУКЦИЯ 	*/"Грузоперевозки."	
		/*МВЗ			*/"Грузоперевозки_"
		/*ПОКАЗАТЕЛИ 	*/"Кол-во с учетом годного выхода"
		)
	/*
			"Расходы на продажу (сч 44)"(
			"бу направления"->"Кол-во"->"Отгрузка."->"ВИДЫ ЗАТРАТ ТЧМ И ТЧМС"->"ПРОДУКЦИЯ ТЧМ и ТЧМС";
			)*/
		ENDFIX
		

			
	SET CREATEBLOCKONEQ OFF;
ENDFIX/*мвз получатель*/

/*отдельно - копирование пок-ля для "СМР для ЛПК" по счету "Цехи п-п (сч 20;23;26)"*/
FIX(									/*из 2.5.03*/
/*ЦФО */
/*МВЗ ПОЛУЧАТЕЛЬ */@RELATIVE("ТЧМ итого (пол-ль)",0)		
/*НАПРАВЛЕНИЕ */"Цехи п-п (сч 20;23;26)"
/*ПРОДУКЦИЯ 	*/"СМР для ЛПК"	
/*МВЗ			*/"УРПП"
/*ПОКАЗАТЕЛИ 	*/
)
				
	datacopy "Кол-во"->"Уч развития подъездных путей" to "Кол-во с учетом годного выхода"->"ПЭО";
ENDFIX	

ENDFIX
/*Удаляем лишний агрегат для переноса контрагентов 2.5.2,2.5.3,2.5.4*/
FIX(
/*СТАТЬИ */"бу статьи"
/*ЦФО */"ПЭО","Уч развития подъездных путей"
/*МВЗ */@RELATIVE("№24. ЖДЦ",0)
/*ВАЛЮТА */"RUR"
/*КОНТРАГЕНТЫ */"Контрагенты ТЧМ и ТЧМС"
/*НАПРАВЛЕНИЕ */"бу направления",@RELATIVE("Всего по направлениям списания",0)
/*ЭЛЕМЕНТЫ_ЗАТРАТ */"бу элемента затрат"
/*СЧЕТА БАЛАНСА */"20_Выбытие"
/*ГОД */{ГОД}
/*ВЕРСИЯ */{ВЕРСИЯ}
/*ПЕРИОД */@RELATIVE("Всего год",0)
/*СЦЕНАРИЙ */{СЦЕНАРИЙ}
/*ПРОДУКЦИЯ */"бу вида продукции",@RELATIVE("Всего по продукции",0)
/*ВИД ЗАТРАТ */"бу видов затрат"
/*ПРОЧЕЕ */@RELATIVE("Всего по видам перевозок",0),"бу прочих справочников"
/*ПОКАЗАТЕЛИ */"Кол-во вагонов на провеску","Кол-во"
/*МВЗ ПОЛУЧАТЕЛЬ */@RELATIVE("ТЧМ итого (пол-ль)",0)
)

CLEARBLOCK ALL;

ENDFIX

/*Удаляем лишний агрегат для переноса продукции 2.5.1 и 2.5.4*/
FIX(
/*СТАТЬИ */"бу статьи"
/*ЦФО */"ПЭО","Уч развития подъездных путей"
/*МВЗ */@RELATIVE("№24. ЖДЦ",0),"Грузоперевозки_" 
/*ВАЛЮТА */"RUR"
/*КОНТРАГЕНТЫ */"Контрагенты ТЧМ и ТЧМС"
/*НАПРАВЛЕНИЕ */"бу направления"
/*ЭЛЕМЕНТЫ_ЗАТРАТ */"бу элемента затрат"
/*СЧЕТА БАЛАНСА */"20_Выбытие"
/*ГОД */{ГОД}
/*ВЕРСИЯ */{ВЕРСИЯ}
/*ПЕРИОД */@RELATIVE("Всего год",0)
/*СЦЕНАРИЙ */{СЦЕНАРИЙ}
/*ПРОДУКЦИЯ */"ПРОДУКЦИЯ ТЧМ и ТЧМС"
/*ВИД ЗАТРАТ */"ВИДЫ ЗАТРАТ ТЧМ И ТЧМС"
/*ПРОЧЕЕ */@RELATIVE("Всего по видам перевозок",0),"бу прочих справочников"
/*ПОКАЗАТЕЛИ */"Кол-во вагонов на провеску","Кол-во"
/*МВЗ ПОЛУЧАТЕЛЬ */@RELATIVE("ТЧМ итого (пол-ль)",0)
)

CLEARBLOCK ALL;

ENDFIX]]></script>
    </rule>
  </rules>
  <components></components>
</HBRRepo>